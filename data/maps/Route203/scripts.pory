const PLAYER = OBJ_EVENT_ID_PLAYER
const FARMER = 11

const VAR_TEMP_X = VAR_TEMP_0
const VAR_TEMP_Y = VAR_TEMP_1

// VAR_SOFTWIND_CITY_STATE values
const ROUTE203_STATE_INIT = 0
const ROUTE203_STATE_MET_FARMER = 1
const ROUTE203_STATE_GOT_MOOCALF = 2

//=====================================
// Map Scripts
//=====================================
mapscripts Route203_MapScripts {}

//=====================================
// Global Scripts
//=====================================
script(global) Route203_RouteSignSoftwindCity {
    msgbox("Route 203\n{LEFT_ARROW} Softwind City", MSGBOX_SIGN)
    end
}

script(global) Route203_RouteSignChartreeForest {
    msgbox("Chartree Forest\n“Tred carefully, preserve the balance”", MSGBOX_SIGN)
    end
}

script(global) Route203_RouteSignFarmhouse {
    msgbox("203 Farmhouse\p“Home of the best Moomoo Milk\nin all of Caldera!”\pNow CLOSED", MSGBOX_SIGN)
    end
}

script(global) Route203_FarmhouseMailbox {
    msgbox(Text_OtherMailbox, MSGBOX_SIGN)
    end
}

script(global) Route203_LittleBoy {
    msgbox(Route203_Text_LittleBoy, MSGBOX_NPC)
    end
}

script(global) Route203_Bard {
    msgbox(Route203_Text_Bard, MSGBOX_NPC)
    end
}

script(global) Route203_Althlete {
    trainerbattle_single(TRAINER_REED, Route203_Text_ReedIntro, Route203_Text_ReedDefeated)
    msgbox(Route203_Text_ReedPostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) Route203_Youngster {
    trainerbattle_single(TRAINER_RYAN, Route203_Text_RyanIntro, Route203_Text_RyanDefeated)
    msgbox(Route203_Text_RyanPostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) Route203_Fisherman {
    trainerbattle_single(TRAINER_ALBERTO, Route203_Text_AlbertoIntro, Route203_Text_AlbertoDefeated)
    msgbox(Route203_Text_AlbertoPostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) Route203_Camper {
    trainerbattle_single(TRAINER_CHARLIE, Route203_Text_CharlieIntro, Route203_Text_CharlieDefeated)
    msgbox(Route203_Text_CharliePostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) Route203_Hiker {
    trainerbattle_single(TRAINER_CLIFTON, Route203_Text_CliftonIntro, Route203_Text_CliftonDefeated)
    msgbox(Route203_Text_CliftonPostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) Route203_FarmerLadyNoticesYou {
    lockall
    setvar(VAR_ROUTE_203_STATE, ROUTE203_STATE_MET_FARMER)
    delay(20)
    playse(SE_PIN)
    applymovement(FARMER, mLookRightStartled)
    waitmovement(0)
    applymovement(FARMER, mFarmerLadyApproaches)
    waitmovement(0)
    getplayerxy(VAR_TEMP_X, VAR_TEMP_Y)
    if(var(VAR_TEMP_X) == 22) {
        applymovement(PLAYER, mFaceDown)
        applymovement(FARMER, mFaceUp)
        waitmovement(0)
    }
    msgbox(Route203_Text_FarmerQuestIntro)
    waitmessage
    applymovement(FARMER, mFaceLeft)
    waitmovement(0)
    msgbox(Route203_Text_FarmerQuestConsider)
    waitmessage
    delay(30)
    if(var(VAR_TEMP_X) == 22) {
        applymovement(FARMER, mFaceUp)
    }
    else {
        applymovement(FARMER, mFaceRight)
    }
    waitmovement(0)
    quest_reveal(QUEST_MOOCALF_ORPHAN)
    msgbox(Route203_Text_FarmerQuestAsk, MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES) {
        quest_start(QUEST_MOOCALF_ORPHAN)
        msgbox(Route203_Text_FarmerQuestThankYou)
        waitmessage
        closemessage
    }
    else {
        msgbox(Route203_Text_FarmerQuestRejected)
        waitmessage
        closemessage
    }
    applymovement(FARMER, mFarmerLadyWalksBack)
    delay(16)
    applymovement(PLAYER, mFaceLeft)
    waitmovement(FARMER)
    releaseall
    end
}

script(global) Route203_FarmerLady {
    lockall
    faceplayer
    quest_check_finished(QUEST_MOOCALF_ORPHAN)
    if(var(VAR_RESULT) == TRUE) {
        if(var(VAR_ROUTE_203_STATE) == ROUTE203_STATE_GOT_MOOCALF) {
            msgbox(Route203_Text_FarmerQuestFinished, MSGBOX_NPC)
        }
        else {
            goto(Route203_FarmerLadyGiveMoocalfStandalone)
        }
    }
    else {
        checkitem(ITEM_MEDICINE_MILK, 1)
        if(var(VAR_RESULT) == TRUE) {
            goto(Route203_FarmerLadyResolveQuest)
        }
        quest_check_started(QUEST_MOOCALF_ORPHAN)
        if(var(VAR_RESULT) == TRUE) {
            msgbox(Route203_Text_FarmerQuestInProgress, MSGBOX_NPC)
        }
        else {
            msgbox(Route203_Text_FarmerQuestSecondChance, MSGBOX_YESNO)
            if(var(VAR_RESULT) == YES) {
                quest_start(QUEST_MOOCALF_ORPHAN)
                msgbox(format("Thank you for seeing sense."))
                waitmessage
            }
            else {
                msgbox(format("I should have known."))
                waitmessage
            }
        }
    }
    releaseall
    end
}

//=====================================
// Local Scripts
//=====================================
script Route203_FarmerLadyResolveQuest {
    lockall
    quest_set_reward_waiting(QUEST_MOOCALF_ORPHAN)
    msgbox(Route203_Text_FarmerQuestTurnIn)
    waitmessage
    applymovement(FARMER, mFaceUp)
    waitmovement(0)
    opendoor(17, 5)
    waitdooranim
    applymovement(FARMER, mFarmerLadyEntersHouse)
    waitmovement(0)
    closedoor(17, 5)
    quest_finish(QUEST_MOOCALF_ORPHAN)
    delay(60)
    opendoor(17, 5)
    waitdooranim
    applymovement(FARMER, mFarmerLadyExitsHouse)
    waitmovement(0)
    closedoor(17, 5)
    goto(Route203_FarmerLadyGiveMoocalfStandalone)
    end
}

script Route203_FarmerLadyGiveMoocalfStandalone {
    lockall
    msgbox(Route203_Text_FarmQuestGiveMoocalf)
    waitmessage
    goto(Route203_FarmerLadyGiveMoocalf)
}

script Route203_FarmerLadyGiveMoocalf {
	setvar(VAR_TEMP_1, SPECIES_MOOCALF)
	givemon(SPECIES_MOOCALF, 7)
    if(var(VAR_RESULT) == MON_GIVEN_TO_PARTY) {
        goto(Route203_FarmerLadyGiveMoocalfParty)
    }
    elif(var(VAR_RESULT) == MON_GIVEN_TO_PC) {
        goto(Route203_FarmerLadyGiveMoocalfPC)
    }
	goto(Common_EventScript_NoMoreRoomForPokemon)
	end
}

script Route203_FarmerLadyGiveMoocalfParty {
	call(Route203_FarmerLadyGiveMoocalfFanfare)
	msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
    if(var(VAR_RESULT) == NO) {
        goto(Route203_FarmerLadyGiveMoocalfEnd)
    }
	call(Common_EventScript_GetGiftMonPartySlot)
	call(Common_EventScript_NameReceivedPartyMon)
	goto(Route203_FarmerLadyGiveMoocalfEnd)
	end
}

script Route203_FarmerLadyGiveMoocalfPC {
	call(Route203_FarmerLadyGiveMoocalfFanfare)
	msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
    if(var(VAR_RESULT) == NO) {
	    goto(Route203_FarmerLadyGiveMoocalfTransferredToPC)
    }
	call(Common_EventScript_NameReceivedBoxMon)
	goto(Route203_FarmerLadyGiveMoocalfTransferredToPC)
	end
}

script Route203_FarmerLadyGiveMoocalfTransferredToPC {
	call(Common_EventScript_TransferredToPC)
	goto(Route203_FarmerLadyGiveMoocalfEnd)
	end
}

script Route203_FarmerLadyGiveMoocalfFanfare {
	bufferspeciesname(STR_VAR_2, SPECIES_MOOCALF)
	playfanfare(MUS_OBTAIN_ITEM)
	message(Route203_Text_ReceivedMoocalf)
	waitmessage
	waitfanfare
	bufferspeciesname(STR_VAR_1, SPECIES_MOOCALF)
	return
}

script Route203_FarmerLadyGiveMoocalfEnd {
    setvar(VAR_ROUTE_203_STATE, ROUTE203_STATE_GOT_MOOCALF)
	releaseall
	end
}

//=====================================
// Text
//=====================================
text Route203_Text_LittleBoy {
    format(
        "After Pinedale Forest got burned up, lots "
        "of new Pokémon came!\pMy sister says some "
        "of them aren't anywhere else in the world!\p"
        "It's called bio… bio…\n"
        "biomalerfacy? No…\p"
        "Oh! \pI remember!\pIt's called biodiversity!"
    )
}

text Route203_Text_Bard {
    format(
        "I'm a storyteller from far away.\pI travelled "
        "here to see this forest's transformation.\p"
        "It must have been an incredible community effort to "
        "contain the flames to just this forest.\p"
        "I can see it now…\p"
        "“And the Pokémon and Trainers did join as one force, unto the flames…”"
    )
}

text Route203_Text_ReedIntro {
    format(
        "Puff… puff…\pI'm training for the… puff… Kalypso City… marathon!\pThis is going to be the… puff… year!"
    )
}

text Route203_Text_ReedDefeated {
    format(
        "Just let me… puff… catch my breath!"
    )
}

text Route203_Text_ReedPostBattle {
    format(
        "Clearly I need more training.\pIf I'm winded after a Pokémon battle, what chance do I have in Kalypso City?"
    )
}

text Route203_Text_RyanIntro {
    format(
        "Bird Pokémon are the best!\nDon't you agree?\pEveryone in my family has raised bird Pokémon. I'll show you why!"
    )
}

text Route203_Text_RyanDefeated {
    format(
        "Maybe I should branch out after all."
    )
}

text Route203_Text_RyanPostBattle {
    format(
        "I've decided, I'm going to try new Pokémon!\pI'll travel across the sea to Johto and catch a Pidgey!"
    )
}

text Route203_Text_AlbertoIntro {
    format(
        "I saw a fellow fisherman down here, and thought there might be good catchin'!\pBut it was just a puddle…\pI know all the best fishing spots, check it out!"
    )
}

text Route203_Text_AlbertoDefeated {
    format(
        "The search continues!\pI'll find even rarer Water Pokémon!"
    )
}

text Route203_Text_AlbertoPostBattle {
    format(
        "I hope that man is okay.\pI think he might have had too much to drink.\pHe dropped his fishing rod as he wandered off mumbling about Sunset Village."
    )
}

text Route203_Text_CharlieIntro {
    format(
        "I love to go camping in Chartree Forest!\pI never get cold, because all the Fire Pokémon keep the forest so warm at night!\pDo you like to camp?"
    )
}

text Route203_Text_CharlieDefeated {
    format(
        "You'll definitely be safe camping wherever you want!"
    )
}

text Route203_Text_CharliePostBattle {
    format(
        "I sometimes wonder what Chartree Forest was like before.\pI think it's better now, though!\pIt's my favorite place."
    )
}

text Route203_Text_CliftonIntro {
    format(
        "Ho ho!\pWhat's a young one like you doing so far from home?\pYou're not properly geared for long distance travel!"
    )
}

text Route203_Text_CliftonDefeated {
    format(
        "Well, maybe you'll be okay after all!"
    )
}

text Route203_Text_CliftonPostBattle {
    format(
        "I hiked over from Mistvale City, but I got bored in Softwind City.\pToo quaint for my tastes.\pCity folk like hiking too, you know!"
    )
}

text Route203_Text_FarmerQuestIntro {
    format(
        "Oh, hello there!\p"
        "I had hoped someone would stop by.\p"
        "My husband and I used to operate "
        "a Miltank farm here, but after the fire…\p"
        "Well, we've only got one young Moocalf left.\p"
        "And now this one has come down sick."
    )
}

text Route203_Text_FarmerQuestConsider {
    format(
        "I just…\pI just don't know what to do."
    )
}

text Route203_Text_FarmerQuestAsk {
    format(
        "Poor Moocalf needs milk, but without the "
        "Miltank around we don't have any.\p"
        "Please, will you find milk for us?"
    )
}

text Route203_Text_FarmerQuestThankYou {
    format(
        "Oh, thank you!\nThank you!\p"
        "I'm afraid I don't know where you "
        "could find milk…\p"
        "I'm sure if you ask around somebody "
        "will be able to help you.\p"
        "Moocalf is counting on you."
    )
}

text Route203_Text_FarmerQuestRejected {
    format(
        "I…\nOkay.\p"
        "That's fine.\p"
        "I'm sure someone more kind will "
        "come by soon."
    )
}

text Route203_Text_FarmerQuestSecondChance {
    format(
        "What, back to laugh at our misfortune?\p"
        "Or have you changed your mind?"
    )
}

text Route203_Text_FarmerQuestInProgress {
    format(
        "Did you find the milk for Moocalf?\p"
        "Oh…\n"
        "I hope you can find it soon…"
    )
}

text Route203_Text_FarmerQuestFinished {
    format(
        "Thank you again for your help.\p"
        "Not a lot of folk your age would take "
        "the time these days.\p"
        "I hope Moocalf is well."
    )
}

text Route203_Text_FarmerQuestTurnIn {
    format(
        "Did you find the milk for Moocalf?\p"
        "You did!?\p"
        "Thank you!\nI'll administer it right away!"
    )
}

text Route203_Text_FarmQuestGiveMoocalf {
    format(
        "Thank you again for your help.\p"
        "My husband and I have gotten too "
        "old to properly care for Moocalf…\p"
        "You're a Pokémon Trainer right?\p"
        "Please, take it with you!"
    )
}

text Route203_Text_ReceivedMoocalf {
    "{PLAYER} obtained a {STR_VAR_2}.$"
}

//=====================================
// Movement
//=====================================

movement mFarmerLadyApproaches {
    walk_down
    walk_right * 5
}

movement mFarmerLadyWalksBack {
    walk_slow_left * 5
    walk_slow_up
    delay_16
    face_down
}

movement mFarmerLadyEntersHouse {
    walk_up
    set_invisible
}

movement mFarmerLadyExitsHouse {
    set_visible
    walk_down
}

//=====================================
// Raw
//=====================================
