const PLAYER = OBJ_EVENT_ID_PLAYER
const BRADLEY = 1
const AMPURE = 2
const MOM = 3

const WARP_RESCUE = 2

// VAR_NEOBAY_STATE values
const NEOBAY_STATE_NEW_GAME = 0
const NEOBAY_STATE_SPOKE_TO_MOM = 1
const NEOBAY_STATE_MET_WILLOW = 2
const NEOBAY_STATE_RECEIVED_STARTER = 3
const NEOBAY_STATE_AMPURE_RESCUED = 4
const NEOBAY_STATE_STARTED_FLASHBACK = 5
const NEOBAY_STATE_SAW_FLASHBACK = 6
const NEOBAY_STATE_RECEIVED_RUNNING_SHOES = 7
const NEOBAY_STATE_DEPARTED = 8
const NEOBAY_STATE_OPTIONAL_RIVAL_DONE = 9

// VAR_AMPURE_RESCUE_STATE values
const AMPURE_RESCUE_STARTED = 1
const AMPURE_RESCUE_IN_MEADOW = 2
const AMPURE_RESCUE_FIRST_BATTLE_DONE = 3
const AMPURE_RESCUE_SECOND_BATTLE_DONE = 4
const AMPURE_RESCUE_SECOND_BATTLE_DONE_WARP = 5
const AMPURE_RESCUE_BRADLEY_FIGHT = 6
const AMPURE_RESCUE_CAPTURE_DONE = 7
const AMPURE_RESCUE_RETURNED = 8

// State descriptions for VAR_NEOBAY_STATE
// 1: Spoke to Mom
// 2: Met Professor Willow
// 3: Received Pokemon
// 4: Saved Ampure
// 5: Received running shoes
// 6: Departed for Sunset Village

// State descriptions for VAR_AMPURE_RESCUE_STATE
// 1: Started mission
// 2: Moved to meadow
// 3: Fought first battle
// 4: Fought second battle
// 5: Interim warp-setup state
// 6: Finished capture
// 7: Returned to Willow

const VAR_TEMP_X = VAR_TEMP_0
const VAR_TEMP_Y = VAR_TEMP_1

//=====================================
// Map Scripts
//=====================================
mapscripts NeoBay_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: NeoBay_OnTransition
    MAP_SCRIPT_ON_LOAD: NeoBay_OnLoad
    MAP_SCRIPT_ON_RESUME: NeoBay_OnResume
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_STARTED: NeoBay_EventScript_SetupBradleyAtBoat
        VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_BRADLEY_FIGHT: NeoBay_EventScript_SetupBradleyForFight
        VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_SECOND_BATTLE_DONE_WARP: NeoBay_EventScript_SetupBradleyForAmpure
    ]
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_STARTED: NeoBay_EventScript_BradleyLeadsToGrass
        FLAG_WILD_ENCOUNTER, 1: NeoBay_OnCompletedWildEncounter
        VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_SECOND_BATTLE_DONE: NeoBay_EventScript_BradleyBattle
    ]
}

//=====================================
// Global Scripts
//=====================================
script NeoBay_EventScript_GoToWillowBradleyTriggerTop {
    lockall
    addobject(BRADLEY)
    applymovement(PLAYER, mFaceLeft)
    setobjectxy(BRADLEY, 12, 18)
    applymovement(BRADLEY, mBradleyApproachPlayerTop)
    waitmovement(0)
    call(NeoBay_EventScript_BradleyIntroText)
    applymovement(BRADLEY, mBradleyEscortsPlayerTop)
    applymovement(PLAYER, mPlayerEscortedByBradleyTop)
    waitmovement(0)
    turnobject(BRADLEY, DIR_WEST)
    call(NeoBay_EventScript_BradleyShowBoatText)
    releaseall
    end
}

script NeoBay_EventScript_GoToWillowBradleyTriggerBottom {
    lockall
    addobject(BRADLEY)
    applymovement(PLAYER, mFaceLeft)
    setobjectxy(BRADLEY, 12, 18)
    applymovement(BRADLEY, mBradleyApproachPlayerBottom)
    waitmovement(0)
    call(NeoBay_EventScript_BradleyIntroText)
    applymovement(BRADLEY, mBradleyEscortsPlayerBottom)
    applymovement(PLAYER, mPlayerEscortedByBradleyBottom)
    waitmovement(0)
    turnobject(BRADLEY, DIR_WEST)
    call(NeoBay_EventScript_BradleyShowBoatText)
    releaseall
    end
}

script NeoBay_EventScript_TownSign {
    msgbox(NeoBay_Text_TownSign, MSGBOX_SIGN)
    end
}

script NeoBay_EventScript_DockSign {
    msgbox(NeoBay_Text_DockSign, MSGBOX_SIGN)
    end
}

script NeoBay_EventScript_PlayerMailbox {
    msgbox(NeoBay_Text_PlayerMailbox, MSGBOX_SIGN)
    end
}

script NeoBay_EventScript_OtherMailbox {
    msgbox(Text_OtherMailbox, MSGBOX_SIGN)
    end
}

script NeoBay_EventScript_BradleyStopsPlayer {
    lockall
    applymovement(BRADLEY, mFaceRight)
    waitmovement(0)
    if(var(VAR_AMPURE_RESCUE_STATE) == AMPURE_RESCUE_BRADLEY_FIGHT) {
        msgbox(format("Bradley: Come on!\nLet's battle!"))
        waitmessage
        closemessage
        getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
        switch(var(VAR_TEMP_1)) {
            case 24:
                applymovement(PLAYER, mPlayerRematchesBradleyAfterLoss1)
            case 25:
                applymovement(PLAYER, mPlayerRematchesBradleyAfterLoss2)
        }
        waitmovement(0)
        goto(NeoBay_EventScript_DoBradleyBattle)
    }
    else {
        msgbox(format("Bradley: What are you doing?\nWe need to find Ampure before it gets away!"))
        waitmessage
        closemessage
        getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
        switch(var(VAR_TEMP_0)) {
            case 9:
                applymovement(PLAYER, mWalkDown)
            case 10:
                applymovement(PLAYER, mWalkDown)
            case 16:
                applymovement(PLAYER, mWalkLeft)
            default:
                applymovement(PLAYER, mWalkLeft)
        }
        waitmovement(PLAYER)
        releaseall
        end
    }
}

script NeoBay_EventScript_BradleyConversation {
    lock
    faceplayer
    switch(var(VAR_AMPURE_RESCUE_STATE)) {
        case AMPURE_RESCUE_BRADLEY_FIGHT:
            goto(NeoBay_EventScript_BradleyBattle)
        default:
            msgbox(format("Bradley: I haven't seen Ampure anywhere.\pYou should check that tall grass!"))
    }
    waitmessage
    closemessage
    release
}

script NeoBay_EventScript_BradleyOptional {
    if(!flag(FLAG_DEFEATED_RIVAL_NEOBAY_2)) {
        lock
        addobject(BRADLEY)
        setobjectxy(BRADLEY, 26, 23)
        applymovement(BRADLEY, mBradleyApproachesForOptionalFight)
        delay(32)
        turnobject(PLAYER, DIR_SOUTH)
        waitmovement(0)
        msgbox(format("Bradley: Hey, what are you up to?\pI'm on my way to grab the things I forgot.\pOh!\nI know!\pLet's have a battle since you're here!"))
        switch(var(VAR_STARTER_MON)) {
            case 0:
                trainerbattle_no_intro(TRAINER_BRADLEY_NEOBAY_2_VENAP, Neobay_Text_BradleyDefeated)
            case 1:
                trainerbattle_no_intro(TRAINER_BRADLEY_NEOBAY_2_CELSINGE, Neobay_Text_BradleyDefeated)
            case 2:
                trainerbattle_no_intro(TRAINER_BRADLEY_NEOBAY_2_ECHOISE, Neobay_Text_BradleyDefeated)
        }
        setflag(FLAG_DEFEATED_RIVAL_NEOBAY_2)
        setvar(VAR_NEOBAY_STATE, NEOBAY_STATE_OPTIONAL_RIVAL_DONE)
        msgbox(format("Bradley: If only I didn't have to go back for my things, I'd be twice as strong!\pI'm gonna pass you up, just you wait!"))
        waitmessage
        closemessage
        applymovement(PLAYER, mWalkLeft)
        waitmovement(0)
        turnobject(PLAYER, DIR_EAST)
        applymovement(BRADLEY, Common_Movement_WalkUp)
        waitmovement(0)
        opendoor(26, 16)
        waitdooranim
        applymovement(BRADLEY, mBradleyLeavesAfterOptionalFight)
        waitmovement(0)
        closedoor(26, 16)
        waitdooranim
        removeobject(BRADLEY)
        release
    }
}

script(global) NeoBay_EventScript_BradleyBattle {
    lockall
    setvar(VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_BRADLEY_FIGHT)
    msgbox(format("Bradley: Still nothing?\nI'm tired of looking…\pWhy don't we have a Pokémon battle while we wait?\pI'll heal your Pokémon first!"))
	playfanfare(MUS_HEAL)
	waitfanfare
	special(HealPlayerParty)
    goto(NeoBay_EventScript_DoBradleyBattle)
    end
}

script(global) NeoBay_EventScript_MomRunningShoes {
    lockall
    setvar(VAR_NEOBAY_STATE, NEOBAY_STATE_RECEIVED_RUNNING_SHOES)
    msgbox("Mom: Wait!\p{PLAYER}!")
    waitmessage
    applymovement(PLAYER, mFaceRight)
    waitmovement(0)
    getplayerxy(VAR_TEMP_X, VAR_TEMP_Y)
    addobject(MOM)
    setobjectxy(MOM, 25, VAR_TEMP_Y)
    applymovement(MOM, mNeoBayMomWalksUpToPlayer)
    waitmovement(0)
    delay(30)
    msgbox(NeoBay_Text_MomAngry)
    waitmessage
	playfanfare(MUS_OBTAIN_ITEM)
	message("{PLAYER} put on the\nRunning Shoes.")
	waitfanfare
	setflag(FLAG_SYS_B_DASH)
    msgbox(Neobay_Text_SayGoodbye)
    waitmessage
    closemessage
    applymovement(MOM, mNeoBayMomStartsToLeave)
    waitmovement(0)
    setflag(FLAG_HIDE_NEO_BAY_MOM)
    removeobject(MOM)
    releaseall
}

//=====================================
// Local Scripts
//=====================================
script(local) NeoBay_OnTransition {
    setflag(FLAG_VISITED_NEOBAY_TOWN)
    call(Common_EventScript_SetupRivalGfxId)
    switch (var(VAR_NEOBAY_STATE)) {
        case NEOBAY_STATE_SPOKE_TO_MOM:
            clearflag(FLAG_HIDE_NEO_BAY_BRADLEY)
        case NEOBAY_STATE_RECEIVED_STARTER:
            clearflag(FLAG_HIDE_NEO_BAY_BRADLEY)
        default:
            setflag(FLAG_HIDE_NEO_BAY_BRADLEY)
    }
}

script(local) NeoBay_OnLoad {
    if(var(VAR_NEOBAY_STATE) == NEOBAY_STATE_SPOKE_TO_MOM) {
        setmetatile(28, 33, METATILE_NeoBay_BridgeReflection_Left, 0)
        setmetatile(29, 33, METATILE_NeoBay_BridgeReflection_Right, 0)
    }
    else {
        setmetatile(28, 33, METATILE_NeoBay_BridgeWarp_Left, 0)
        setmetatile(29, 33, METATILE_NeoBay_BridgeWarp_Right, 0)
    }
}

script(local) NeoBay_OnResume {
    if (flag(FLAG_WILD_ENCOUNTER)) {
        switch(var(VAR_AMPURE_RESCUE_STATE)) {
            case AMPURE_RESCUE_IN_MEADOW:
                call(NeoBay_UpdateBradleyPositionAfterEncounter)
            case AMPURE_RESCUE_FIRST_BATTLE_DONE:
                call(NeoBay_UpdateBradleyPositionAfterEncounter)
        }
    }
}

script(local) NeoBay_EventScript_BradleyIntroText {
    msgbox(NeoBay_Text_BradleyIntroStart, MSGBOX_DEFAULT)
    applymovement(BRADLEY, mBradleyFidgetsRight)
    waitmovement(0)
    msgbox(NeoBay_Text_BradleyIntroStartLetsGo, MSGBOX_DEFAULT)
    subquest_finish(SUB_QUEST_FIND_BRADLEY)
    waitmessage
    closemessage
    return
}

script(local) NeoBay_EventScript_BradleyShowBoatText {
    msgbox(NeoBay_Text_BradleyShowBoatStart, MSGBOX_DEFAULT)
    waitmessage
    closemessage
    applymovement(BRADLEY, mEnterShip)
    applymovement(PLAYER, mEnterShip)
    waitmovement(0)
    setflag(FLAG_SPAWN_INVISIBLE)
    warp(MAP_NEO_BAY_PROFESSOR_WILLOWS_LAB, 0xFF, 8, 2)
    return
}

script(local) NeoBay_EventScript_BradleyLeadsToGrass {
    lockall
    applymovement(BRADLEY, mBradleyWalksUpAndLooksAtPlayer)
    applymovement(PLAYER, mPlayerWalksUpAndLooksAtBradley)
    waitmovement(PLAYER)
    msgbox(format("Bradley: I think I see some tracks here.\nThey're heading towards… the meadow?\pI wonder why he'd go there…"))
    msgbox(format("{PLAYER}, I think there may be wild Pokémon where we're going, so be ready!"), MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(BRADLEY, mBradleyEscortsPlayerToMeadow)
    applymovement(PLAYER, mPlayerEscortsBradleyToMeadow)
    waitmovement(PLAYER)
    applymovement(BRADLEY, mBradleyLooksAroundAndFidgets)
    waitmovement(BRADLEY)
    msgbox(format("Bradley: Help me look for Ampure, {PLAYER}!\pHe's probably hiding in this tall grass.\pIf you walk around enough, he should appear!"), MSGBOX_AUTOCLOSE)
    waitmessage
    setvar(VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_IN_MEADOW)
    copyobjectxytoperm(BRADLEY)
    releaseall
    end
}

script(local) NeoBay_EventScript_SetupBradleyAtBoat {
	setrespawn(HEAL_LOCATION_NEO_BAY)
    addobject(BRADLEY)
    setobjectxy(BRADLEY, 28, 33)
    turnobject(BRADLEY, DIR_NORTH)
    turnobject(PLAYER, DIR_NORTH)
    return
}

script(local) NeoBay_OnCompletedWildEncounter {
    clearflag(FLAG_WILD_ENCOUNTER)
    switch(var(VAR_AMPURE_RESCUE_STATE)) {
        case AMPURE_RESCUE_IN_MEADOW:
            call(NeoBay_EventScript_AmpureRescueAfterFirst)
        case AMPURE_RESCUE_FIRST_BATTLE_DONE:
            call(NeoBay_EventScript_AmpureRescueAfterSecond)
    }
    return
}

script(local) NeoBay_UpdateBradleyPositionAfterEncounter {
    special(PrintTest)
    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
    addvar(VAR_TEMP_0, 1)
    setobjectxy(BRADLEY, VAR_TEMP_0, VAR_TEMP_1)
    copyobjectxytoperm(BRADLEY)
    turnobject(BRADLEY, DIR_WEST)
    return
}

script(local) NeoBay_EventScript_AmpureRescueAfterFirst {
    lockall
    setvar(VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_FIRST_BATTLE_DONE)
    applymovement(PLAYER, mFaceRight)
    waitmovement(PLAYER)
    msgbox(format("Bradley: Did you find him?\n… … …\pOh, well keep looking.\pHere, I'll heal your Pokémon for you."))
    waitmessage
    closemessage
    call(Common_EventScript_OutOfCenterPartyHeal)
    releaseall
    return
}

script(local) NeoBay_EventScript_AmpureRescueAfterSecond {
    lockall
    applymovement(PLAYER, mFaceRight)
    waitmovement(PLAYER)
    msgbox(format("Bradley: Any luck this time?\n… … …\pOh, well keep looking.\pHere, I'll heal your Pokémon again."))
	playfanfare(MUS_HEAL)
	waitfanfare
    closemessage
	special(HealPlayerParty)
    setvar(VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_SECOND_BATTLE_DONE_WARP)
    warp(MAP_NEO_BAY, 0xFF, 12, 23)
    releaseall
    return
}

script(local) NeoBay_EventScript_SetupBradleyForAmpure {
    addobject(BRADLEY)
    addobject(AMPURE)
    setobjectxy(BRADLEY, 11, 23)
    turnobject(BRADLEY, DIR_EAST)
    turnobject(PLAYER, DIR_WEST)
    setvar(VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_SECOND_BATTLE_DONE)
}

script(local) NeoBay_EventScript_SetupBradleyForFight {
    addobject(BRADLEY)
    setobjectxyperm(BRADLEY, 11, 23)
}

script(local) NeoBay_EventScript_AmpureCapture {
    lockall
    setvar(VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_CAPTURE_DONE)
    addobject(AMPURE)
    applymovement(AMPURE, mAmpureRunsUpToPlayer)
    waitmovement(AMPURE)
    delay(48)
    playse(SE_PIN)
    applymovement(AMPURE, mExclamationPoint)
    applymovement(BRADLEY, mLookUpStartled)
    applymovement(PLAYER, mLookUpStartled)
    waitmovement(PLAYER)
    playmoncry(SPECIES_AMPURE, CRY_MODE_NORMAL)
    waitmoncry
    msgbox(format("Bradley: I think he likes you, {PLAYER}!\pJust stay still, I'll catch him!"))
    waitmessage
    applymovement(BRADLEY, mWalkUpLookRight)
    waitmovement(BRADLEY)
	special(SavePlayerParty)
	special(PutRivalMonInPlayerParty)
    special(StartBradleyAmpureCatch)
    waitstate
    delay(60)
    fadescreen(FADE_TO_WHITE)
    removeobject(AMPURE)
    setobjectxy(BRADLEY, 12, 22)
    turnobject(BRADLEY, DIR_SOUTH)
	special(LoadPlayerParty)
    fadescreen(FADE_FROM_WHITE)
    msgbox(format("Bradley: ALRIGHT! And that was my first time, too!\pLet's get back to Prof. Willow with the good news!"))
    waitmessage
    subquest_finish(SUB_QUEST_RESCUE_AMPURE)
    warp(MAP_NEO_BAY_PROFESSOR_WILLOWS_LAB, 0xFF, 8, 6)
    releaseall
    return
}

script(local) NeoBay_EventScript_DoBradleyBattle {
    msgbox(format("Bradley: Alright!\pI'm shaking with excitement!\nHere goes…\p{PLAYER}, I challenge you to a Pokémon battle!\pWow… that felt so good to finally say!"))
    closemessage
    switch(var(VAR_STARTER_MON)) {
        case 0:
            trainerbattle_no_intro(TRAINER_BRADLEY_NEOBAY_1_VENAP, Neobay_Text_BradleyDefeated2)
        case 1:
            trainerbattle_no_intro(TRAINER_BRADLEY_NEOBAY_1_CELSINGE, Neobay_Text_BradleyDefeated2)
        case 2:
            trainerbattle_no_intro(TRAINER_BRADLEY_NEOBAY_1_ECHOISE, Neobay_Text_BradleyDefeated2)
    }
    goto(NeoBay_EventScript_AmpureCapture)
}

//=====================================
// Text
//=====================================
text NeoBay_Text_TownSign {
    format(
        "Neo Bay\n"
        "“The most isolated town in Caldera. Where new things begin.”"
    )
}

text NeoBay_Text_DockSign {
    format(
        "Neo Bay Dock\n"
        "“The only access point for regional visitors, rare though they are.”"
    )
}

text NeoBay_Text_PlayerMailbox {
    format(
        "{PLAYER}'s Mailbox:\n"
        "Looks like there's no mail today…"
    )
}

text(global) Text_OtherMailbox {
    format(
        "I shouldn't go snooping through other people's mail!"
    )
}

text NeoBay_Text_BradleyIntroStart {
    format(
        "Bradley: Well, if it isn't {PLAYER}!\p"
        "So you've finally woken up, sleepyhead?\p"
        "Honestly, you've got a whole lot of nerve, making a friend wait like that!\p"
        "… … Nah, I'm just kidding!\nI'm not that bad of a guy, y'know?\p"
        "Anyway!"
    )
}

text NeoBay_Text_BradleyIntroStartLetsGo {
    format(
        "Bradley: I called you here because I've got "
        "a once-in-a-lifetime opportunity for ya!\p"
        "There's someone you absolutely, definitely, positively HAVE to meet!\p"
        "Trust me, it'll be worth your time.\nI promise!\p"
        "Come on, come on! This way!"
    )
}

text NeoBay_Text_BradleyShowBoatStart {
    format(
        "Bradley: Look!\nYou've noticed this sleek-looking"
        "boat moored here for a while now, right?\p"
        "Well, this boat belongs to none other than Prof. Willow!\p"
        "… … …\n"
        "… … …\p"
        "What, you haven't heard of her?\n"
        "I guess you're not quite as worldly as your pal Bradley!\p"
        "… Anyway, she's a world-famous Pokémon Professor, "
        "known for her research on ancient Pokémon fossils!\p"
        "She's cool, calm, composed…\n"
        "and when it comes to Pokémon, she's a genius!\p"
        "I really admire her!\n"
        "Come on, let's go meet her now!"
    )
}

text Neobay_Text_BradleyDefeated {
    format(
        "Hah, I knew I was right!"
    )
}

text Neobay_Text_BradleyDefeated2 {
    format(
        "Wow, that good already?"
    )
}

text (global) Placeholder_Text {
    "Placeholder"
}

text NeoBay_Text_MomAngry {
    format (
        "Mom: That professor woman came looking for you.\p"
        "She said you're leaving to search for Simon.\n"
        "Is that true?\p"
        "… … …\p"
        "And you weren't even going to say goodbye…\p"
        "I…\n"
        "I guess I can't stop you.\p"
        "Please… please come back to visit…\p"
        "At least take these with you."
    )
}

text Neobay_Text_SayGoodbye {
    format(
        "Mom: If you're ever in danger, or you want to come home, "
        "these running shoes will help you move much faster.\p"
        "I'll miss you, {PLAYER}.\p"
    )
}

//=====================================
// Movement
//=====================================
movement mBradleyApproachPlayerTop {
    walk_down * 5
    walk_right
    walk_down
    walk_right * 4
}

movement mBradleyApproachPlayerBottom {
    walk_down * 6
    walk_right
    walk_down
    walk_right * 4
}

movement mBradleyFidgetsRight {
    walk_in_place_fast_right * 4
}

movement mBradleyFidgetsLeft {
    walk_in_place_fast_left * 4
}

movement mBradleyFidgetsDown {
    walk_in_place_fast_down * 4
}

movement mBradleyFidgetsUp {
    walk_in_place_fast_up * 4
}

movement mBradleyEscortsPlayerTop {
    walk_down
    walk_right * 8
    walk_down * 6
    walk_right * 4
}

movement mBradleyEscortsPlayerBottom {
    walk_up
    walk_right * 8
    walk_down * 7
    walk_right * 4
}

movement mPlayerEscortedByBradleyTop {
    walk_left
    walk_down
    walk_right * 8
    walk_down * 6
    walk_right * 3
}

movement mPlayerEscortedByBradleyBottom {
    walk_left
    walk_up
    walk_right * 8
    walk_down * 7
    walk_right * 3
}

movement mEnterShip {
    walk_down * 2
    set_invisible
}

movement mBradleyWalksUpAndLooksAtPlayer {
    walk_up
    walk_in_place_slow_right
}

movement mPlayerWalksUpAndLooksAtBradley {
    walk_up
    walk_in_place_slow_left
}

movement mBradleyEscortsPlayerToMeadow {
    walk_up * 3
    walk_left * 4
    walk_up * 4
    walk_left * 12
    walk_up
}

movement mPlayerEscortsBradleyToMeadow {
    walk_up * 3
    walk_left * 5
    walk_up * 4
    walk_left * 12
    walk_in_place_slow_up
}

movement mBradleyLooksAroundAndFidgets {
    walk_in_place_slow_right
    delay_16 * 4
    walk_left
    walk_up * 2
    walk_in_place_slow_left
    delay_16 * 3
    walk_in_place_slow_up
    delay_16 * 3
    walk_in_place_slow_right
    delay_16 * 3
    walk_in_place_slow_up
    delay_16 * 3
    walk_in_place_fast_down * 4
    walk_left
    delay_16 * 3
    walk_in_place_slow_up
    delay_16 * 3
    walk_in_place_slow_left
    delay_16 * 3
    walk_right
    walk_down
}

movement mWalkLeft {
    walk_left
}

movement mAmpureRunsUpToPlayer {
    walk_fast_down * 8
}

movement mLookUpStartled {
    emote_exclamation_mark
    walk_in_place_slow_up
}

movement mWalkUpLookRight {
    walk_up
    walk_in_place_slow_right
}

movement mBradleyApproachesForOptionalFight {
    walk_up * 5
}

movement mBradleyLeavesAfterOptionalFight {
    walk_up
    set_invisible
}

movement mPlayerRematchesBradleyAfterLoss1 {
    walk_left * 3
    walk_up
    walk_left
}

movement mPlayerRematchesBradleyAfterLoss2 {
    walk_left * 3
    walk_up * 2
    walk_left
}

movement mNeoBayMomWalksUpToPlayer {
    walk_left * 7
}

movement mNeoBayMomStartsToLeave {
    walk_slow_right * 4
    delay_16 * 4
    walk_in_place_slow_left
    delay_16 * 4
    walk_slow_right * 4
    set_invisible
}

//=====================================
// Raw
//=====================================