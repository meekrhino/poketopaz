const PLAYER = OBJ_EVENT_ID_PLAYER
const FLOWER_GUY = 15

const VAR_TEMP_X = VAR_TEMP_0
const VAR_TEMP_Y = VAR_TEMP_1

// VAR_ROUTE_205_STATE values
const ROUTE205_STATE_INIT = 0
const ROUTE205_STATE_OUTSIDE_SHOP = 1
const ROUTE205_STATE_INSIDE_SHOP = 2

//=====================================
// Map Scripts
//=====================================
mapscripts Route205_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_ROUTE_205_STATE, ROUTE205_STATE_INSIDE_SHOP: Route205_FlowerGuyComesOut
    ]
}

//=====================================
// Global Scripts
//=====================================
script(global) Route205_RichBoy {
    msgbox(Route205_Text_RichBoy, MSGBOX_NPC)
    end
}

script(global) Route205_Girl {
    msgbox(Route205_Text_Girl, MSGBOX_NPC)
    end
}

script(global) Route205_Scientist {
    msgbox(Route205_Text_Scientist, MSGBOX_NPC)
    end
}

script(global) Route205_Woman {
    msgbox(Route205_Text_Woman, MSGBOX_NPC)
    end
}

script(global) Route205_FlowerGirl {
    msgbox(format("I have a friend that lives in the Kanto region.\pShe said they have a Pokémon that's really similar to Ampure.\pPeek-o-something?\nisn't that silly?"), MSGBOX_NPC)
    end
}

script(global) Route205_FlowerShopGuard {
    lockall
    faceplayer
    if(var(VAR_ROUTE_205_STATE) == ROUTE205_STATE_INIT) {
        msgbox(format("Just keep moving, kid.\nNothing to see here!\p… … …\n… … …\pOh, who am I kidding, you're just a kid.\pWhat harm could you do?\pCome on in, I suppose."))
        setvar(VAR_ROUTE_205_STATE, ROUTE205_STATE_OUTSIDE_SHOP)
    }
    else {
        msgbox(format("Ah, it's you again.\pPlease come on inside."))
    }
    waitmessage
    closemessage
    applymovement(FLOWER_GUY, mFaceUp)
    waitmovement(FLOWER_GUY)
    opendoor(34, 5)
    waitdooranim
    applymovement(FLOWER_GUY, mFlowerGuyEntersHouse)
    delay(30)
    applymovement(PLAYER, mPlayerEntersHouse)
    waitmovement(PLAYER)
    closedoor(34, 5)
    waitdooranim
    warp(MAP_ROUTE205_FLOWER_SHOP, 0, 0, 0)
    releaseall
    end
}

script(global) Route205_FlowerGuyComesOut {
    lockall
    setvar(VAR_ROUTE_205_STATE, ROUTE205_STATE_OUTSIDE_SHOP)
    delay(30)
    applymovement(PLAYER, mFaceUp)
    waitmovement(PLAYER)
    opendoor(34, 5)
    waitdooranim
    addobject(FLOWER_GUY)
    setobjectxy(FLOWER_GUY, 34, 5)
    msgbox(format("Excuse me, please."))
    waitmessage
    closemessage
    applymovement(PLAYER, mPlayerSlidesDown)
    applymovement(FLOWER_GUY, mWalkDown)
    waitmovement(FLOWER_GUY)
    closedoor(34, 5)
    waitdooranim
    msgbox("Come back any time.\pJust don't spill the beans…\nErr, that is…\pKeep your face shut!\nOr… um… else!\p…How was that?")
    waitmessage
    clearflag(FLAG_HIDE_ROUTE_205_FLOWER_GUY)
    releaseall
    end
}

script(global) Route205_Francis {
    trainerbattle_single(TRAINER_FRANCIS, Route205_Text_FrancisIntro, Route205_Text_FrancisDefeated)
    msgbox(Route205_Text_FrancisPostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) Route205_Amy {
    trainerbattle_double(TRAINER_AMY_AND_ALLY, Route205_Text_AmyAndAllyIntro, Route205_Text_AmyAndAllyDefeated, Route205_Text_AmyAndAllyTooFewMons)
    msgbox(Route205_Text_AmyPostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) Route205_Ally {
    trainerbattle_double(TRAINER_AMY_AND_ALLY, Route205_Text_AmyAndAllyIntro, Route205_Text_AmyAndAllyDefeated, Route205_Text_AmyAndAllyTooFewMons)
    msgbox(Route205_Text_AllyPostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) Route205_Travis {
    trainerbattle_single(TRAINER_TRAVIS, Route205_Text_TravisIntro, Route205_Text_TravisDefeated)
    msgbox(Route205_Text_TravisPostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) Route205_Gwen {
    trainerbattle_single(TRAINER_GWEN, Route205_Text_GwenIntro, Route205_Text_GwenDefeated)
    msgbox(Route205_Text_GwenPostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) Route205_Percy {
    trainerbattle_single(TRAINER_PERCY, Route205_Text_PercyIntro, Route205_Text_PercyDefeated)
    msgbox(Route205_Text_PercyPostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) Route205_Ishmael {
    trainerbattle_single(TRAINER_ISHMAEL, Route205_Text_IshmaelIntro, Route205_Text_IshmaelDefeated)
    msgbox(Route205_Text_IshmaelPostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) Route205_FlowerShop {
    msgbox(format("Small Cottage\nOnly For Friends"), MSGBOX_SIGN)
    end
}

script(global) Route205_DEBUG {
    givemon(SPECIES_CHIARAME, 5)
    givemon(SPECIES_SERBERINE, 5)
    end
}

//=====================================
// Text
//=====================================
text Route205_Text_RichBoy {
    format(
        "My friend and I are arguing about what "
        "Therma will bring back this time.\p"
        "I just know it's going to be new histories for "
        "the library!"
    )
}

text Route205_Text_Girl {
    format(
        "My friend just cannot stop gossiping about "
        "what Therma will bring back.\pBut since you "
        "asked, I am certain it will be new research "
        "papers!"
    )
}

text Route205_Text_Scientist {
    format(
        "The best time of every season is waiting "
        "for Therma's new books!\pI am giddy as a "
        "Gashawk right now!"
    )
}

text Route205_Text_Woman {
    format(
        "I've been waiting to challenge Therma "
        "since before she left!\pThe first time "
        "I battled her she won handily…\p"
        "She's incredibly strong!\p"
        "This time is going to be different, though!"
    )
}

text Route205_Text_FrancisIntro {
    format(
        "My teacher has been talking about Pokémon adaptation in Caldera.\pApparently my Pokémon came from far away a long time ago!"
    )
}

text Route205_Text_FrancisDefeated {
    format(
        "I guess Lotad adapted to its environment, but not to battle…"
    )
}

text Route205_Text_FrancisPostBattle {
    format(
        "They say Lotad are actually water type in other regions.\pHow weird is that?"
    )
}

text Route205_Text_AmyAndAllyIntro {
    format(
        "Hehe!\pWanna see some cute Pokémon?"
    )
}

text Route205_Text_AmyAndAllyDefeated {
    format(
        "W…why would you hurt our cute Pokémon?"
    )
}

text Route205_Text_AmyPostBattle {
    format(
        "It's okay…"
    )
}

text Route205_Text_AllyPostBattle {
    format(
        "You hurt Amy's feelings!\pWhy would you do that?"
    )
}

text Route205_Text_AmyAndAllyTooFewMons {
    format (
        "Only one Pokémon?\nUgh, lame.\pWe only battle together, so now you can't see our cute Pokémon."
    )
}

text Route205_Text_TravisIntro {
    format(
        "Huff…puff…\nI feel unstoppable right now!\pBring it on!"
    )
}

text Route205_Text_TravisDefeated {
    format(
        "I lost, but that won't keep me down!"
    )
}

text Route205_Text_TravisPostBattle {
    format(
        "I'm just taking a quick break to catch my breath.\pWe had a great match, didn't we?"
    )
}

text Route205_Text_GwenIntro {
    format(
        "What a beautiful day!\pWould you care to sit and enjoy nature with me?"
    )
}

text Route205_Text_GwenDefeated {
    format(
        "So relaxing!\pIt's hard to be down from a loss when surrounded by such beauty."
    )
}

text Route205_Text_GwenPostBattle {
    format(
        "I love to watch the Jalsida gather nectar from the flowers.\pNature is magical!"
    )
}

text Route205_Text_PercyIntro {
    format(
        "I just evolved my first Pokémon ever!\pYou'll see what that means for you!"
    )
}

text Route205_Text_PercyDefeated {
    format(
        "There is no way I lost that.\nYou must have cheated."
    )
}

text Route205_Text_PercyPostBattle {
    format(
        "My mom bought me so many Rare Candies to evolve Firrel and I still lost?\pMaybe…\nNahhh.\pI just need more!"
    )
}

text Route205_Text_IshmaelIntro {
    format(
        "Ahoy there!\pI finally managed to find the holy grail of all fisherman.\pIt's worth its weight in gold!"
    )
}

text Route205_Text_IshmaelDefeated {
    format(
        "I didn't say it was strong, did I?"
    )
}

text Route205_Text_IshmaelPostBattle {
    format(
        "Where did I catch it?\nHah!\pLike I would spill my secrets with a random child?\pYou've probably never heard of the place anyway."
    )
}

text Route205_Text_DebugDefeatedEnemy1 {
    format (
        "I'm enemy 1 and you win!"
    )
}

text Route205_Text_DebugDefeatedEnemy2 {
    format (
        "I'm enemy 2 and you win!"
    )
}


//=====================================
// Movement
//=====================================
movement mFlowerGuyEntersHouse {
    walk_up
    set_invisible
}

movement mPlayerEntersHouse {
    walk_up * 2
    set_invisible
}

movement mPlayerSlidesDown {
    lock_facing_direction
    walk_down
    unlock_facing_direction
}

//=====================================
// Raw
//=====================================
