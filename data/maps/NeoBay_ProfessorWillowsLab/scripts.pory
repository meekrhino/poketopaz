const PLAYER = OBJ_EVENT_ID_PLAYER
const VENAP_BALL = 1
const CELSINGE_BALL = 2
const ECHOISE_BALL = 3
const WILLOW = 4
const BRADLEY = 5
const AMPURE = 6

const DOOR_POS_X = 8
const DOOR_POS_Y = 2

// VAR_NEOBAY_STATE values
const NEOBAY_STATE_NEW_GAME = 0
const NEOBAY_STATE_SPOKE_TO_MOM = 1
const NEOBAY_STATE_MET_WILLOW = 2
const NEOBAY_STATE_RECEIVED_STARTER = 3
const NEOBAY_STATE_AMPURE_RESCUED = 4
const NEOBAY_STATE_STARTED_FLASHBACK = 5
const NEOBAY_STATE_SAW_FLASHBACK = 6
const NEOBAY_STATE_RECEIVED_RUNNING_SHOES = 7
const NEOBAY_STATE_DEPARTED = 8
const NEOBAY_STATE_OPTIONAL_RIVAL_DONE = 9

// VAR_AMPURE_RESCUE_STATE values
const AMPURE_RESCUE_STARTED = 1
const AMPURE_RESCUE_IN_MEADOW = 2
const AMPURE_RESCUE_FIRST_BATTLE_DONE = 3
const AMPURE_RESCUE_SECOND_BATTLE_DONE = 4
const AMPURE_RESCUE_SECOND_BATTLE_DONE_WARP = 5
const AMPURE_RESCUE_BRADLEY_FIGHT = 6
const AMPURE_RESCUE_CAPTURE_DONE = 7
const AMPURE_RESCUE_RETURNED = 8

//=====================================
// Map Scripts
//=====================================
mapscripts NeoBay_ProfessorWillowsLab_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: WillowsLab_OnTransition
    MAP_SCRIPT_ON_LOAD: WillowsLab_OnLoad
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_NEOBAY_STATE, NEOBAY_STATE_SPOKE_TO_MOM: WillowIntroduction
        VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_CAPTURE_DONE: WillowsLab_AmpureRescued
        VAR_NEOBAY_STATE, NEOBAY_STATE_RECEIVED_RUNNING_SHOES: WillowsLab_Departure
        VAR_NEOBAY_STATE, NEOBAY_STATE_STARTED_FLASHBACK: WillowsLab_AfterFlashback
    ]
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_NEOBAY_STATE, NEOBAY_STATE_NEW_GAME {
            removeobject(BRADLEY)
            end
        }
        VAR_NEOBAY_STATE, NEOBAY_STATE_AMPURE_RESCUED {
            removeobject(BRADLEY)
            end
        }
        VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_CAPTURE_DONE: WillowsLab_OnWarp
        VAR_NEOBAY_STATE, NEOBAY_STATE_RECEIVED_RUNNING_SHOES: WillowsLab_OnWarp
    ]
}

//=====================================
// Global Scripts
//=====================================
script(global) WillowsLab_YouGoFirst {
    if (var(VAR_NEOBAY_STATE) == NEOBAY_STATE_SAW_FLASHBACK) {
        msgbox(format("Bradley: I guess we'd better go talk to our folks like the Prof. said…"), MSGBOX_NPC)
        end
    }
    msgbox(format("Bradley: I just can't decide!\nYou go first, {PLAYER}!\pCome on, come on, come on!"), MSGBOX_NPC)
    end
}

script(global) WillowsLab_TakeYourTime {
    if (var(VAR_NEOBAY_STATE) == NEOBAY_STATE_SAW_FLASHBACK) {
        lockall
        faceplayer
	    msgbox(format("Willow: So, you stopped by your family to say goodbye, did you?"), MSGBOX_YESNO)
	    if(var(VAR_RESULT) == YES) {
            msgbox(format("Willow: … … …\n… … …\pYou haven't even left my lab.\pGet going before I regret giving you a Pokémon!"), MSGBOX_NPC)
        }
        else {
            msgbox(format("Willow: Better get going!\pIf you're not back in time, we'll meet you at Sunset Village.\pI believe Route 201 is open now, if you'd rather take that path."))
        }
        releaseall
    }
    else {
        msgbox(format("Willow: Ohoho, can't decide?\pTake your time, this is a big decision!"), MSGBOX_NPC)
    }
    end
}

script(global) WillowsLab_Books1 {
    msgbox(format("There's a book titled “People and Pokémon: Nature's Balance”…\pSounds a little dense…"), MSGBOX_SIGN)
    end
}

script(global) WillowsLab_Books2 {
    msgbox(format("There's a book titled “Ancient Pokémon Mythos”…\pSounds intruguing!"), MSGBOX_SIGN)
    end
}

script(global) WillowsLab_Books3 {
    msgbox(format("There's a book titled “The Lifecycles of Bug Pokémon”…\pSounds gross!"), MSGBOX_SIGN)
    end
}

script(global) WillowsLab_Books4 {
    msgbox(format("There's a book titled “The Varied Pokémon of Mt. Frost”…\pSounds like an interesting place!"), MSGBOX_SIGN)
    end
}

script(global) WillowsLab_Books5 {
    msgbox(format("There are tons of dusty textbooks…\pIt looks like Prof. Willow hasn't used this shelf in a while."), MSGBOX_SIGN)
    end
}

script(global) WillowsLab_WillowsComputer {
    msgbox(format("This is Prof. Willow's personal computer.\pThe screen is covered in graphs and data!"), MSGBOX_SIGN)
}

script(global) WillowsLab_WillowsNotes {
    msgbox(format("Looks like a daily log of field notes. Prof. Willow sure is thorough!"), MSGBOX_SIGN)
}

script(global) WillowsLab_ExpensiveMachine {
    msgbox(format("Looks like an expensive machine.\pHopefully Ampure didn't break anything!"), MSGBOX_SIGN)
}

script(global) WillowsLab_TrashCan {
    msgbox(format("It's filled to the brim with crumpled notes."), MSGBOX_SIGN)
}

script(global) WillowsLab_EventScript_SelectVenap {
    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
    if(var(VAR_TEMP_1) != 10) {
        return
    }
    release
    if(var(VAR_NEOBAY_STATE) != NEOBAY_STATE_MET_WILLOW) {
        goto(WillowsLab_DontBeGreedy)
    }
    playmoncry(SPECIES_VENAP, CRY_MODE_NORMAL)
    waitmoncry
    showmonpic(SPECIES_VENAP, 10, 3)
    msgbox(WillowsLab_DoYouWantVenap, MSGBOX_YESNO)
    waitmessage
    if(var(VAR_RESULT) == NO) {
        goto(WillowsLab_TakeYourTime2)
    }
    goto(WillowsLab_GiveVenap)
}

script(global) WillowsLab_EventScript_SelectCelsinge {
    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
    if(var(VAR_TEMP_1) != 10) {
        return
    }
    release
    if(var(VAR_NEOBAY_STATE) != NEOBAY_STATE_MET_WILLOW) {
        goto(WillowsLab_DontBeGreedy)
    }
    playmoncry(SPECIES_CELSINGE, CRY_MODE_NORMAL)
    waitmoncry
    showmonpic(SPECIES_CELSINGE, 10, 3)
    msgbox(WillowsLab_DoYouWantCelsinge, MSGBOX_YESNO)
    waitmessage
    if(var(VAR_RESULT) == NO) {
        goto(WillowsLab_TakeYourTime2)
    }
    goto(WillowsLab_GiveCelsinge)
}

script(global) WillowsLab_EventScript_SelectEchoise {
    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
    if(var(VAR_TEMP_1) != 10) {
        return
    }
    release
    if(var(VAR_NEOBAY_STATE) != NEOBAY_STATE_MET_WILLOW) {
        goto(WillowsLab_DontBeGreedy)
    }
    playmoncry(SPECIES_ECHOISE, CRY_MODE_NORMAL)
    waitmoncry
    showmonpic(SPECIES_ECHOISE, 10, 3)
    msgbox(WillowsLab_DoYouWantEchoise, MSGBOX_YESNO)
    waitmessage
    if(var(VAR_RESULT) == NO) {
        goto(WillowsLab_TakeYourTime2)
    }
    goto(WillowsLab_GiveEchoise)
}

script(global) WillowsLab_EventScript_WhereAreYouGoing {
    lock
    msgbox(format("Bradley: Hey!\nWhere are you going?\pHurry up and choose a Pokémon already!"), MSGBOX_SIGN)
    waitmessage
    applymovement(PLAYER, mWalkDown)
    waitmovement(PLAYER)
    release
    end
}

//=====================================
// Local Scripts
//=====================================
script(local) WillowsLab_OnTransition {
    if (var(VAR_NEOBAY_STATE) == NEOBAY_STATE_SPOKE_TO_MOM) {
        setobjectxyperm(WILLOW, 2, 8)
        setobjectmovementtype(WILLOW, MOVEMENT_TYPE_FACE_LEFT)
        clearflag(FLAG_WILLOW_BALL_TAKEN_0)
        clearflag(FLAG_WILLOW_BALL_TAKEN_1)
        clearflag(FLAG_WILLOW_BALL_TAKEN_2)
        removeobject(BRADLEY)
    }
    elif(var(VAR_AMPURE_RESCUE_STATE) == AMPURE_RESCUE_CAPTURE_DONE
      || var(VAR_NEOBAY_STATE) == NEOBAY_STATE_STARTED_FLASHBACK
      || var(VAR_NEOBAY_STATE) == NEOBAY_STATE_SAW_FLASHBACK) {
        addobject(BRADLEY)
        setobjectxyperm(BRADLEY, 8, 7)
        setobjectmovementtype(BRADLEY, MOVEMENT_TYPE_FACE_RIGHT)
        setobjectxyperm(WILLOW, 11, 6)
        setobjectmovementtype(WILLOW, MOVEMENT_TYPE_FACE_LEFT)
    }
    elif(var(VAR_NEOBAY_STATE) == NEOBAY_STATE_RECEIVED_RUNNING_SHOES) {
        setobjectxyperm(WILLOW, 2, 8)
        setobjectmovementtype(WILLOW, MOVEMENT_TYPE_FACE_RIGHT)
        removeobject(BRADLEY)
    }
}

script(local) WillowsLab_OnLoad {
    if(var(VAR_NEOBAY_STATE) == NEOBAY_STATE_SPOKE_TO_MOM) {
        setmetatile(8, 2, METATILE_Lab_Door_WillowsLabNoWarp, 0)
    }
    else {
        setmetatile(8, 2, METATILE_Lab_Door_WillowsLabExit, 0)
    }
}

script(local) WillowsLab_OnWarp {
    if(var(VAR_AMPURE_RESCUE_STATE) == AMPURE_RESCUE_CAPTURE_DONE
    || var(VAR_NEOBAY_STATE) == NEOBAY_STATE_STARTED_FLASHBACK
    || var(VAR_NEOBAY_STATE) == NEOBAY_STATE_SAW_FLASHBACK) {
        turnobject(PLAYER, DIR_EAST)
        turnobject(BRADLEY, DIR_EAST)
        turnobject(WILLOW, DIR_WEST)
    }
}

script(local) WillowIntroduction {
    lockall
    setvar(VAR_NEOBAY_STATE, NEOBAY_STATE_MET_WILLOW)
    setvar(VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_STARTED)
    setmetatile(8, 2, METATILE_Lab_Door_WillowsLabExit, 0)
    special(DrawWholeMapView)
    opendoor(DOOR_POS_X, DOOR_POS_Y)
    waitdooranim
    setmetatile(8, 1, METATILE_Lab_Door_WillowsLabOpenTop, 0)
    setmetatile(8, 2, METATILE_Lab_Door_WillowsLabOpenBottom, 0)
    special(DrawWholeMapView)
    addobject(BRADLEY)
    setobjectxy(BRADLEY, DOOR_POS_X, DOOR_POS_Y)
    applymovement(BRADLEY, mBradleyEntersFirstTime)
    delay(16)
    applymovement(PLAYER, mEntersDoorFromTop)
    waitmovement(0)
    applymovement(PLAYER, mPlayerEntersFirstTime)
    waitmovement(0)
    msgbox("Bradley: Hey, Prof. Willow!", MSGBOX_AUTOCLOSE)
    applymovement(WILLOW, mFaceUp)
    waitmovement(WILLOW)
    applymovement(AMPURE, mAmpureMakesCrash)
    waitmovement(AMPURE)
    setvar(VAR_0x8004, 0)  // vertical pan
    setvar(VAR_0x8005, 4)  // horizontal pan
    setvar(VAR_0x8006, 2)  // num shakes
    setvar(VAR_0x8007, 10)  // shake delay
    special(ShakeCamera)
    waitstate
    msgbox("CRASH!!", MSGBOX_AUTOCLOSE)
    waitmessage
    playse(SE_PIN)
    applymovement(BRADLEY, mExclamationPoint)
    applymovement(PLAYER, mExclamationPoint)
    applymovement(WILLOW, mFaceDown)
    waitmovement(0)
    msgbox(format("Willow: Come on, Ampure!\nThat's fragile!\pGet back in your Pokéball!"), MSGBOX_AUTOCLOSE)
    applymovement(AMPURE, mAmpureRunsAround)
    applymovement(BRADLEY, mBradleyRushesUp)
    delay(15)
    applymovement(PLAYER, mPlayerWalksDown)
    setvar(VAR_0x8004, 0)   // vertical pan
    setvar(VAR_0x8005, 1)   // horizontal pan
    setvar(VAR_0x8006, 20)  // num shakes
    setvar(VAR_0x8007, 5)   // shake delay
    special(ShakeCamera)
    waitstate
    waitmovement(AMPURE)
    msgbox(format("Bradley: Prof. Willow! What's going on!?"), MSGBOX_AUTOCLOSE)
    playse(SE_PIN)
    applymovement(AMPURE, mExclamationPoint)
    waitmovement(0)
    applymovement(AMPURE, mAmpureRushesOut)
    applymovement(WILLOW, mFaceRight)
    waitmovement(AMPURE)
    playmoncry(SPECIES_AMPURE, CRY_MODE_NORMAL)
    waitmoncry
    msgbox(format("Willow: Watch out!"), MSGBOX_AUTOCLOSE)
    applymovement(AMPURE, mAmpureRushesOut2)
    delay(64)
    applymovement(BRADLEY, mFaceRight)
    applymovement(PLAYER, mFaceLeft)
    delay(16)
    applymovement(BRADLEY, mFaceUp)
    applymovement(PLAYER, mFaceUp)
    waitmovement(AMPURE)
    msgbox(format("Willow: Oh, not again…\pThat little rascal just will not keep still!"))
    applymovement(BRADLEY, mFaceLeft)
    applymovement(PLAYER, mFaceLeft)
    waitmovement(BRADLEY)
    msgbox(format("Bradley: Are you alright, Prof. Willow?"))
    msgbox(format("Willow: Ohohoho!\pThanks for showing concern, Bradley, but I'm fine.\pThis happens all the time."), MSGBOX_AUTOCLOSE)
    playse(SE_PIN)
    applymovement(WILLOW, mLookLeftStartled)
    waitmovement(0)
    applymovement(WILLOW, mWillowComesUpToFriends)
    delay(16)
    applymovement(BRADLEY, mFaceDown)
    delay(32)
    applymovement(PLAYER, mFaceDown)
    waitmovement(0)
    msgbox(format("Oh? And who are you?"))
    msgbox(format("Bradley: This is {PLAYER}.\pThey've been my friend for as long as I can remember."))
    msgbox(WillowsLab_WillowIntroduction, MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(BRADLEY, mFaceRight)
    delay(15)
    applymovement(PLAYER, mFaceLeft)
    waitmovement(PLAYER)
    msgbox(WillowsLab_BradleyIsAnAssistant)
    applymovement(BRADLEY, mFaceDown)
    applymovement(PLAYER, mFaceDown)
    waitmovement(PLAYER)
    msgbox(format("Willow: Did I now?\n… Ah, I remember now!\pOhoho, you're quite right!"), MSGBOX_AUTOCLOSE)
    applymovement(WILLOW, mWillowGoesToTable)
    waitmovement(WILLOW)
    msgbox(format("Willow: There are three Pokéballs on this table.\pEach one contains an extremely rare Pokémon.\pBradley, go ahead and choose whichever you like!"))
    msgbox("Bradley: Actually…", MSGBOX_AUTOCLOSE)
    applymovement(BRADLEY, mBradleyDeliberates)
    waitmovement(BRADLEY)
    msgbox("Bradley: I want {PLAYER} to have the\nPokémon.", MSGBOX_AUTOCLOSE)
    playse(SE_PIN)
    applymovement(PLAYER, mExclamationPoint)
    applymovement(PLAYER, mFaceLeft)
    applymovement(WILLOW, mExclamationPoint)
    waitmovement(WILLOW)
    msgbox(format("Willow: But Bradley!\nYou earned this Pokémon yourself!\lYou deserve it!"))
    msgbox(format("Bradley: No, I made up my mind a long time ago.\pI want {PLAYER} to have the Pokémon!"), MSGBOX_AUTOCLOSE)
    applymovement(WILLOW, mFaceRight)
    applymovement(PLAYER, mFaceDown)
    waitmovement(WILLOW)
    msgbox(WillowsLab_WillowCantJustGivePokemon)
    msgbox(WillowsLab_BradleyIsReallySure, MSGBOX_AUTOCLOSE)
    waitmessage
    delay(60)
    applymovement(WILLOW, mFaceUp)
    waitmovement(WILLOW)
    msgbox(WillowsLab_WillowConcedes)
    msgbox(format("Alright, I've decided.\pBoth of you will receive a Pokémon!"))
    applymovement(BRADLEY, mBradleyFidgetsDown)
    msgbox(format("Bradley: Wow, Prof. Willow!\nReally!?"))
    msgbox(format("Willow: Ohoho, how can I refuse such a sincere request?\pGo ahead and choose any Pokéball on the table."))
    msgbox(format("Bradley: This is awesome!\nThank you so much, Prof. Willow!"), MSGBOX_AUTOCLOSE)
    applymovement(BRADLEY, mFaceRight)
    delay(15)
    applymovement(PLAYER, mFaceLeft)
    waitmovement(BRADLEY)
    msgbox(format("Bradley: I'm so excited!\nYou go first, {PLAYER}.\pCome on come on, choose one!"))
    waitmessage
    closemessage
    copyobjectxytoperm(BRADLEY)
    releaseall
    end
}

script(local) WillowsLab_DontBeGreedy {
    msgbox(WillowsLab_DontBeGreedyText, MSGBOX_SIGN)
    releaseall
    end
}

script(local) WillowsLab_TakeYourTime2 {
	hidemonpic
    msgbox(format("Willow: Take your time, this is a big decision!"), MSGBOX_NPC)
    releaseall
    end
}

script(local) WillowsLab_GiveVenap {
    bufferspeciesname(0, SPECIES_VENAP)
    setvar(VAR_TEMP_1, SPECIES_VENAP)
    givemon(SPECIES_VENAP, 5, ITEM_NONE)
    setmonmetlocation(0, MAPSEC_WILLOWS_LAB)
    removeobject(VENAP_BALL)
    call(WillowsLab_ReceivedStarter)
    setflag(FLAG_WILLOW_BALL_TAKEN_0)
    setvar(VAR_STARTER_MON, 0)
    call(WillowsLab_BradleyChoosesEchoise)
    call(WillowsLab_RegroupAfterPlayerChoosesVenap)
    goto(WillowsLab_YourePokemonTrainersNow)
}

script(local) WillowsLab_GiveCelsinge {
    bufferspeciesname(0, SPECIES_CELSINGE)
    setvar(VAR_TEMP_1, SPECIES_CELSINGE)
    givemon(SPECIES_CELSINGE, 5, ITEM_NONE)
    setmonmetlocation(0, MAPSEC_WILLOWS_LAB)
    removeobject(CELSINGE_BALL)
    call(WillowsLab_ReceivedStarter)
    setflag(FLAG_WILLOW_BALL_TAKEN_1)
    setvar(VAR_STARTER_MON, 1)
    call(WillowsLab_BradleyChoosesVenap)
    call(WillowsLab_RegroupAfterPlayerChoosesCelsinge)
    goto(WillowsLab_YourePokemonTrainersNow)
}

script(local) WillowsLab_GiveEchoise {
    bufferspeciesname(0, SPECIES_ECHOISE)
    setvar(VAR_TEMP_1, SPECIES_ECHOISE)
    givemon(SPECIES_ECHOISE, 5, ITEM_NONE)
    setmonmetlocation(0, MAPSEC_WILLOWS_LAB)
    removeobject(ECHOISE_BALL)
    call(WillowsLab_ReceivedStarter)
    setflag(FLAG_WILLOW_BALL_TAKEN_2)
    setvar(VAR_STARTER_MON, 2)
    call(WillowsLab_BradleyChoosesCelsinge)
    call(WillowsLab_RegroupAfterPlayerChoosesEchoise)
    goto(WillowsLab_YourePokemonTrainersNow)
}

script(local) WillowsLab_ReceivedStarter {
    playfanfare(MUS_OBTAIN_ITEM)
    message("{PLAYER} received the {STR_VAR_1}\nfrom Prof. Willow!")
    waitmessage
    waitfanfare
    closemessage
    msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES) {
        call(Common_EventScript_GetGiftMonPartySlot)
        call(Common_EventScript_NameReceivedPartyMon)
    }
    waitmessage
    closemessage
    setvar(VAR_NEOBAY_STATE, NEOBAY_STATE_RECEIVED_STARTER)
    hidemonpic
    return
}

script(local) WillowsLab_BradleyChoosesVenap {
    applymovement(BRADLEY, mBradleyWalksToVenap)
    waitmovement(BRADLEY)
    msgbox(format("Bradley: I've made up my mind.\pI'll take this one!"))
    bufferspeciesname(0, SPECIES_VENAP)
    setvar(VAR_TEMP_1, SPECIES_VENAP)
    removeobject(VENAP_BALL)
    call(WillowsLab_BradleyMadeChoice)
    setflag(FLAG_WILLOW_BALL_TAKEN_0)
    return
}

script(local) WillowsLab_BradleyChoosesCelsinge {
    applymovement(BRADLEY, mBradleyWalksToCelsinge)
    waitmovement(BRADLEY)
    msgbox(format("Bradley: I've made up my mind.\pI'll take this one!"))
    bufferspeciesname(0, SPECIES_CELSINGE)
    setvar(VAR_TEMP_1, SPECIES_CELSINGE)
    removeobject(CELSINGE_BALL)
    call(WillowsLab_BradleyMadeChoice)
    setflag(FLAG_WILLOW_BALL_TAKEN_1)
    return
}

script(local) WillowsLab_BradleyChoosesEchoise {
    applymovement(BRADLEY, mBradleyWalksToEchoise)
    waitmovement(BRADLEY)
    msgbox(format("Bradley: I've made up my mind.\pI'll take this one!"))
    bufferspeciesname(0, SPECIES_ECHOISE)
    setvar(VAR_TEMP_1, SPECIES_ECHOISE)
    removeobject(ECHOISE_BALL)
    call(WillowsLab_BradleyMadeChoice)
    setflag(FLAG_WILLOW_BALL_TAKEN_2)
    return
}

script(local) WillowsLab_BradleyMadeChoice {
    message("Bradley received the {STR_VAR_1}\nfrom Prof. Willow!")
    waitmessage
    delay(60)
    closemessage
    return
}

script(local) WillowsLab_RegroupAfterPlayerChoosesVenap {
    applymovement(PLAYER, mPlayerRegroupsAfterVenap)
    applymovement(BRADLEY, mBradleyRegroupsAfterEchoise)
    delay(30)
    applymovement(WILLOW, mWillowRegroupsAfterStarter)
    waitmovement(WILLOW)
    return
}

script(local) WillowsLab_RegroupAfterPlayerChoosesCelsinge {
    applymovement(PLAYER, mPlayerRegroupsAfterCelsinge)
    applymovement(BRADLEY, mBradleyRegroupsAfterVenap)
    delay(30)
    applymovement(WILLOW, mWillowRegroupsAfterStarter)
    waitmovement(WILLOW)
    return
}

script(local) WillowsLab_RegroupAfterPlayerChoosesEchoise {
    applymovement(PLAYER, mPlayerRegroupsAfterEchoise)
    applymovement(BRADLEY, mBradleyRegroupsAfterCelsinge)
    delay(30)
    applymovement(WILLOW, mWillowRegroupsAfterStarter)
    waitmovement(WILLOW)
    return
}

script(local) WillowsLab_YourePokemonTrainersNow {
    setflag(FLAG_SYS_POKEMON_GET)
    msgbox(format("Willow: Ohohoho, the looks on your faces are priceless!\pFrom now on, both of you are officially Pokémon Trainers!\lCongratulations!"))
    msgbox(format("Bradley: You hear that, {PLAYER}?\nWe're legit Pokémon Trainers now!\pWe're going straight to the big leagues!"))
    msgbox(format("Willow: Ahem!\nBefore scampering off with your new Pokémon, there's something I'd like you to do for me in return."))
    msgbox(format("Bradley: Anything, professor!"))
    msgbox(WillowsLab_AmpureMissionDescription)
    msgbox(format("Bradley: Not a problem, Prof. Willow!\pWe'll have your Ampure back before you know it!"), MSGBOX_AUTOCLOSE)
    waitmessage
    applymovement(BRADLEY, mFaceRight)
    applymovement(PLAYER, mFaceLeft)
    waitmovement(PLAYER)
    delay(30)
    msgbox(format("Bradley: Let's go, {PLAYER}!\pWe owe the professor, so let's go search for her Ampure!\pCome on, race ya there!"))
    waitmessage
    closemessage
    applymovement(BRADLEY, mBradleyExits)
    applymovement(PLAYER, mPlayerExits)
    waitmovement(PLAYER)
    subquest_finish(SUB_QUEST_CHOOSE_POKEMON)
    warp(MAP_NEO_BAY, 1, 0, 0)
    releaseall
    end
}

script(local) WillowsLab_AmpureRescued {
    lockall
    setvar(VAR_AMPURE_RESCUE_STATE, AMPURE_RESCUE_RETURNED)
    setvar(VAR_NEOBAY_STATE, NEOBAY_STATE_AMPURE_RESCUED)
    applymovement(BRADLEY, mBradleyFidgetsRight)
    waitmovement(BRADLEY)
    msgbox(format("Bradley: We did it professor!\n{PLAYER} found Ampure and I caught it!"))
    msgbox(format("Willow: Ohohohoho!\nI'm glad.\pIt would have taken me ages to find Ampure by myself.\pThank you very much, you two."))
    waitmessage
    closemessage
    applymovement(WILLOW, mWillowDeliberates)
    waitmovement(WILLOW)
    msgbox(format("Willow: I think I have the perfect way to reward you.\pEach of you can take one of these."))
    waitmessage
    closemessage
    call(WillowsLab_ReceivedPokedex)
    msgbox(format("Bradley: This is one of those Pokédex things, right?"))
    msgbox(WillowsLab_PokedexDescription)
    waitmessage
    closemessage
    applymovement(BRADLEY, mBradleyFidgetsUp)
    waitmovement(BRADLEY)
    msgbox("Bradley: Did you hear that, {PLAYER}!?\pWe're finally going to be able\nto find Simon.")
    waitmessage
    closemessage
    fadescreen(FADE_TO_BLACK)
    msgbox("Willow: Who is this Simon?")
    msgbox("Bradley: … … …\nHe was our friend…\p…and he was my brother.")
    waitmessage
    closemessage
    call(Script_SetGrayscaleTint)
    clearflag(FLAG_HIDE_WILLOWS_LAB_BRADLEY)
    warpsilent(MAP_NEO_BAY_FLASHBACK_BEACH, 0, 0, 0)
    releaseall
    end
}

script(local) WillowsLab_AfterFlashback {
    lockall
    setvar(VAR_NEOBAY_STATE, NEOBAY_STATE_SAW_FLASHBACK)
    turnobject(PLAYER, DIR_EAST)
    msgbox(format("Bradley: By the time either of us looked back, he was gone…\pWe never saw him again."))
    closemessage
    delay(60)
    msgbox("Willow: I… I'm so sorry.")
    msgbox(format("Bradley: That's why we have to get to the next town, Professor!\pI just know Simon is out there somewhere, and he could still be in danger."))
    quest_start(QUEST_FINDING_SIMON)
    msgbox(WillowsLab_SayGoodbyeToMom, MSGBOX_AUTOCLOSE)
    releaseall
    end
}

script(local) WillowsLab_ReceivedPokedex {
	playfanfare(MUS_OBTAIN_ITEM)
	message("{PLAYER} received the Pokédex!")
	waitfanfare
	setflag(FLAG_SYS_POKEDEX_GET)
	special(SetUnlockedPokedexFlags)
	setflag(FLAG_RECEIVED_POKEDEX_FROM_WILLOW)
	return
}

script(local) WillowsLab_Departure {
    lockall
    setvar(VAR_NEOBAY_STATE, NEOBAY_STATE_DEPARTED)
    delay(30)
    applymovement(PLAYER, mPlayerEntersSecondTime)
    opendoor(DOOR_POS_X, DOOR_POS_Y)
    waitdooranim
    addobject(BRADLEY)
    setobjectxy(BRADLEY, DOOR_POS_X, DOOR_POS_Y)
    applymovement(BRADLEY, mEntersDoorFromTop)
    waitmovement(0)
    closedoor(DOOR_POS_X, DOOR_POS_Y)
    waitdooranim
    applymovement(BRADLEY, mBradleyEntersSecondTime)
    waitmovement(0)
    delay(30)
    setvar(VAR_0x8004, 0)  // vertical pan
    setvar(VAR_0x8005, 2)  // horizontal pan
    setvar(VAR_0x8006, 2)  // num shakes
    setvar(VAR_0x8007, 5)  // shake delay
    special(ShakeCamera)
    waitstate
    applymovement(BRADLEY, mFaceRight)
    applymovement(PLAYER, mFaceLeft)
    waitmovement(PLAYER)
    msgbox(WillowsLab_HowWasYourMom)
    waitmessage
    applymovement(BRADLEY, mFaceDown)
    applymovement(PLAYER, mFaceDown)
    waitmovement(PLAYER)
    msgbox(WillowsLab_NotABadIdea)
    waitmessage
    msgbox(WillowsLab_DescribePokemonLeague)
    waitmessage
    msgbox(WillowsLab_FroyLivesAway)
    waitmessage
    applymovement(BRADLEY, mBradleyFidgetsDown)
    msgbox(format("Bradley: I can't wait until we get to land!"))
    waitmessage
    msgbox(format("Willow: Ohohoho!\pThat's good, Bradley, because we've arrived!"))
    waitmessage
    setvar(VAR_0x8004, 0)  // vertical pan
    setvar(VAR_0x8005, 2)  // horizontal pan
    setvar(VAR_0x8006, 2)  // num shakes
    setvar(VAR_0x8007, 5)  // shake delay
    special(ShakeCamera)
    waitstate
    playse(SE_DING_DONG)
    waitse
    closemessage
    applymovement(WILLOW, mWillowExitsInSunset)
    applymovement(BRADLEY, mBradleyExitsInSunset)
    applymovement(PLAYER, mPlayerExitsInSunset)
    waitmovement(PLAYER)
    opendoor(DOOR_POS_X, DOOR_POS_Y)
    waitdooranim
    applymovement(WILLOW, mWillowHoldsDoor)
    waitmovement(WILLOW)
    applymovement(WILLOW, mWillowWalkOutDoorInSunset)
    applymovement(BRADLEY, mBradleyWalkOutDoorInSunset)
    applymovement(PLAYER, mPlayerWalkOutDoorInSunset)
    waitmovement(WILLOW)
    closedoor(DOOR_POS_X, DOOR_POS_Y)
    waitdooranim
    warp(MAP_SUNSET_VILLAGE, 0xFF, 11, 34)
    releaseall
    end
}


//=====================================
// Text
//=====================================
text WillowsLab_WillowIntroduction {
    format(
        "Willow: Ohoho, so you're {PLAYER}!\nBradley talks about you quite a lot.\p"
        "You can call me Prof. Willow.\pI'm here studying Pokémon habitats in the Neo Bay area.\pResearch is what I do best.\p"
        "I'm not too good at handling Pokémon themselves, unfortunately…"
    )
}

text WillowsLab_BradleyIsAnAssistant {
    format(
        "Bradley: I never told you this, but I have been helping PROF. WILLOW with her research.\p"
        "Collecting samples, taking measurements, all that science-y stuff.\p"
        "It's hard work, but in return she promised me a rare Pokémon!"
    )
}

text WillowsLab_WillowCantJustGivePokemon {
    format(
        "Willow: Even so, I can't just hand a Pokémon to anybody!\p"
        "Owning a Pokémon comes with many responsibilities.\p"
        "Bradley, you've already shown me that "
        "you're capable of becoming a Pokémon Trainer.\p"
        "I only met {PLAYER} today!"
    )
}

text WillowsLab_BradleyIsReallySure {
    format(
        "Bradley: {PLAYER} has wanted to be a Pokémon Trainer their entire life!\p"
        "Nobody is more passionate and responsible than them, and they'd definitely "
        "make a better Pokémon Trainer than I would!\p"
        "Please, professor.\nTrust me on this one!"
    )
}

text WillowsLab_WillowConcedes {
    format(
        "… … … …\n"
        "… … … …\l"
        "… … … …\l"
        "… … … …\p"
        "… Ohohohohoho! I see, I see.\nSo that's how it is!\p"
        "You're a subborn one, Bradley,\nbut I like that.\p"
        "{PLAYER}, you're lucky to have such a good friend.\p"
        "I must say, I'm a bit envious myself!"
    )
}

text WillowsLab_DontBeGreedyText {
    format(
        "You already chose a Pokémon!\nBetter not be greedy."
    )
}

text WillowsLab_DoYouWantVenap {
    format(
        "Prof. Willow: The Grass Pokémon Venap caught your eye?\p"
        "It might be difficult to raise…\nbut you won't regret it!\p"
        "Is Venap your final decision?"
    )
}

text WillowsLab_DoYouWantCelsinge {
    format(
        "Prof. Willow: The Fire Pokémon Celsinge is your pick?\p"
        "You can't go wrong there!\nYou'll be set up for success!\p"
        "Do you want to choose Celsinge?"
    )
}

text WillowsLab_DoYouWantEchoise {
    format(
        "Prof. Willow: The Water Pokémon Echoise is your favorite?\p"
        "Excellent choice!\nIsn't it just the cutest?\p"
        "Are you certain you want Echoise?"
    )
}

text WillowsLab_AmpureMissionDescription {
    format(
        "Willow: Very well.\p"
        "As you saw earlier, my Ampure has escaped…\p…again.\p"
        "I'd go find him myself, but with my luck,\n"
        "it would take a whole day for me to catch him.\p"
        "That's where you two come in!\p"
        "Please find my Ampure and bring him back to me.\p"
        "He couldn't have wandered off too far, "
        "so try searching between here and Sunset Village.\p"
        "As long as you have your Pokémon, you'll be safe in the tall grass.\p"
        "I entrust you two with bringing Ampure back safely!"
    )
}

text WillowsLab_PokedexDescription {
    format(
        "Willow: Yes, Bradley.\p"
        "In fact, I'm pretty sure that they're "
        "the only ones to ever come to Caldera or Kirant.\p"
        "Prof. Oak of the Kanto region gave these to me when he heard I "
        "was coming here.\p"
        "But my research has prevented me from filling them properly.\p"
        "I think that you two will be perfect for the job.\p"
        "Your Pokémon are already quite fond of you.\p"
        "You should have no problem catching and trading "
        "Pokémon beyond this town!"
    )
}

text WillowsLab_SayGoodbyeToMom {
    format(
        "Willow: I've never seen such determination "
        "as I see right now in both of you.\p"
        "I'd be more than happy to take you across the "
        "water to Sunset Village.\p"
        "But before we leave you much promise "
        "me you will say goodbye to your families."
    )
}

text WillowsLab_HowWasYourMom {
    format(
        "Bradley: How was your mom?\p"
        "… … …\n… … …\p"
        "Yeah…\n"
        "Mine was pretty sad too, but she's sure we'll be fine.\p"
        "Who knows?\n"
        "Maybe we'll return as Pokémon Champions "
        "and our parents will never have to worry about us again!"
    )
}

text WillowsLab_NotABadIdea {
    format(
        "Willow: That isn't a bad idea, Bradley.\p"
        "{PLAYER}, have you ever thought of taking the Pokémon League challenge?\p"
        "Not only will your Pokémon grow along the way, "
        "it will help you complete your Pokédex and take you "
        "all around the regions in search of your friend.\p"
        "You could achive all of your goals at the same time."
    )
}

text WillowsLab_DescribePokemonLeague {
    format(
        "Bradley: Yeah, {PLAYER}!\n"
        "That will be perfect!\p"
        "Here, I'll explain how the League works in our region.\p"
        "Unlike other places, our League has 12 gyms!\p"
        "Six gyms here in Caldera, and six in Kirant.\p"
        "If you defeat all 12 and collect the badges they give "
        "you, you'll be allowed into Triumph Spire, where the "
        "Elite Four await…\p"
        "But we have a LONG way to go before that happens!"
    )
}

text WillowsLab_FroyLivesAway {
    format(
        "Willow: I'm only taking you as far as Sunset Village.\p"
        "The first gym leader, Froy, lives in Cranewing City.\p"
        "That's another two towns over."
    )
}

//=====================================
// Movement
//=====================================
movement mBradleyEntersFirstTime {
    set_visible
    walk_down * 4
    walk_left * 3
    face_down
}

movement mBradleyEntersSecondTime {
    walk_down * 4
    walk_left * 3
    walk_up
    face_down
}

movement mEntersDoorFromTop {
    set_visible
    walk_down
}

movement mPlayerEntersFirstTime {
    delay_16 * 2
    walk_down * 3
    walk_left * 2
    face_down
}

movement mPlayerEntersSecondTime {
    walk_down * 3
    walk_left * 2
    face_down
}

movement mExclamationPoint {
    emote_exclamation_mark
}

movement mFaceDown {
    face_down
}

movement mFaceUp {
    face_up
}

movement mFaceRight {
    face_right
}

movement mFaceLeft {
    face_left
}

movement mAmpureMakesCrash {
    walk_in_place_fast_left * 4
    slide_right
    walk_faster_left
}

movement mAmpureRunsAround {
    walk_fast_right
    walk_fast_down
    walk_faster_right * 9
    walk_fast_up
    walk_fast_down
    walk_fast_up
    walk_fast_down
    walk_fast_up
    walk_fast_down
    walk_faster_left * 6
    walk_faster_up
    walk_faster_left * 3
}

movement mBradleyRushesUp {
    walk_fast_left
    walk_fast_down
}

movement mPlayerWalksDown {
    walk_fast_down
}

movement mAmpureRushesOut {
    walk_up * 2
    walk_left
    face_up
}

movement mAmpureRushesOut2 {
    walk_in_place_fast_up * 8
    walk_faster_up * 3
    walk_faster_right * 3
    walk_faster_up * 4
    set_invisible
}

movement mLookLeftStartled {
    emote_exclamation_mark
    face_left
}

movement mWillowComesUpToFriends {
    walk_down
    walk_right * 4
    face_up
}

movement mWillowGoesToTable {
    walk_down * 3
    walk_right * 2
}

movement mBradleyDeliberates {
    face_right
    delay_16 * 4
    face_down
}

// Bradley goes to pick a starter
movement mBradleyWalksToVenap {
    walk_down * 2
    walk_right * 2
    walk_down
    walk_right
    face_up
}

movement mBradleyWalksToCelsinge {
    walk_down * 2
    walk_right * 2
    walk_down
    walk_right * 2
    face_up
}

movement mBradleyWalksToEchoise {
    walk_down * 2
    walk_right * 2
    walk_down * 2
    walk_right * 3
    walk_up
}

// Regrouping up left with willow after picking
movement mBradleyRegroupsAfterVenap {
    walk_left
    walk_up * 2
    walk_left * 2
    walk_up
    face_down
}

movement mBradleyRegroupsAfterCelsinge {
    walk_left * 2
    walk_up * 2
    walk_left * 2
    walk_up
    face_down
}

movement mBradleyRegroupsAfterEchoise {
    walk_left * 3
    walk_up * 2
    walk_left * 2
    walk_up
    face_down
}

movement mPlayerRegroupsAfterVenap {
    walk_left
    walk_up * 3
    walk_left
    face_down
}

movement mPlayerRegroupsAfterCelsinge {
    walk_left * 2
    walk_up * 3
    walk_left
    face_down
}

movement mPlayerRegroupsAfterEchoise {
    walk_left * 3
    walk_up * 3
    walk_left
    face_down
}

movement mWillowRegroupsAfterStarter {
    walk_left * 2
    walk_up * 2
    walk_left * 2
    walk_up
}

movement mWalkDown {
    walk_down
}

movement mBradleyExits {
    walk_up
    walk_right * 4
    walk_up * 4
    set_invisible
}

movement mPlayerExits {
    walk_left
    walk_up
    walk_right * 4
    walk_up * 4
    set_invisible
}

movement mWillowDeliberates {
    face_right
    delay_16 * 4
    face_left
}

movement mPlayerExitsInSunset {
    delay_16 * 8
    walk_right * 2
    walk_up
}

movement mBradleyExitsInSunset {
    delay_16 * 6
    walk_up
    walk_right * 3
    walk_up
}

movement mWillowExitsInSunset {
    walk_right * 6
    walk_up * 5
}

movement mWillowHoldsDoor {
    walk_right
    face_left
    delay_16 * 2
}

movement mPlayerWalkOutDoorInSunset {
    walk_up * 3
    set_invisible
}

movement mBradleyWalkOutDoorInSunset {
    walk_up * 2
    set_invisible
}

movement mWillowWalkOutDoorInSunset {
    delay_16 * 3
    walk_left
    walk_up * 1
    set_invisible
}

//=====================================
// Raw
//=====================================
