const PLAYER = OBJ_EVENT_ID_PLAYER
const CAMERA = OBJ_EVENT_ID_CAMERA
const VAR_X_TEMP = VAR_TEMP_0
const VAR_Y_TEMP = VAR_TEMP_1

const BRADLEY = 3
const SHOOK = 2
const ALLARD = 1

// VAR_ROUTE204_STATE values
const ROUTE204_STATE_INIT = 0
const ROUTE204_STATE_DEFEATED_FROY = 1
const ROUTE204_STATE_DEFEATED_SHOOK_ALLARD = 2

// VAR_CHARTREE_FOREST_STATE values
const CHARTREE_FOREST_INIT = 0
const CHARTREE_FOREST_MET_FROY = 1
const CHARTREE_FOREST_ROCKETS_IN_FOREST = 2
const CHARTREE_FOREST_ROCKETS_DEFEATED = 3

//=====================================
// Map Scripts
//=====================================
mapscripts Route204_MapScripts {}

//=====================================
// Global Scripts
//=====================================
script(global) Route204_RouteSignChartreeForest {
    msgbox("Chartree Forest\nPlease watch your step.", MSGBOX_SIGN)
    end
}

script(global) Route204_RouteSignCranewingCity {
    msgbox("Route 204\n{DOWN_ARROW} Cranewing City", MSGBOX_SIGN)
    end
}

script(global) Route204_Boy {
    if(var(VAR_ROUTE204_STATE) != ROUTE204_STATE_DEFEATED_SHOOK_ALLARD) {
        msgbox(format("I saw some shady looking people in black lurking around the forest.\pI think Froy chased them away for now."), MSGBOX_NPC)
    }
    else {
        msgbox(format("Phew, I think those goons are gone for good this time.\pWell done!\nYou must be some trainer!"), MSGBOX_NPC)
    }
    end
}

script(global) Route204_Beauty {
    trainerbattle_single(TRAINER_AINA, Route204_Text_AinaIntro, Route204_Text_AinaDefeated)
    msgbox(Route204_Text_AinaPostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) Route204_RichBoy {
    trainerbattle_single(TRAINER_DERRICK, Route204_Text_DerrickIntro, Route204_Text_DerrickDefeated)
    msgbox(Route204_Text_DerrickPostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) Route204_Athlete {
    trainerbattle_single(TRAINER_LEXI, Route204_Text_LexiIntro, Route204_Text_LexiDefeated)
    msgbox(Route204_Text_LexiPostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) Route204_ExpertWoman {
    trainerbattle_single(TRAINER_BEATRICE, Route204_Text_BeatriceIntro, Route204_Text_BeatriceDefeated)
    msgbox(Route204_Text_BeatricePostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) Route204_MeetShookAndAllard {
    lock
    addobject(SHOOK)
    applymovement(SHOOK, mShookAndAllardApproach1)
    delay(60)
    addobject(ALLARD)
    applymovement(ALLARD, mShookAndAllardApproach1)
    waitmovement(0)
    playse(SE_PIN)
    applymovement(SHOOK, mExclamationPoint)
    applymovement(ALLARD, mExclamationPoint)
    waitmovement(0)
    msgbox("Grunt 1: Hey, you!")
    closemessage
    applymovement(SHOOK, mShookAndAllardApproach2)
    applymovement(ALLARD, mShookAndAllardApproach2)
    waitmovement(0)
    applymovement(ALLARD, mFaceLeft)
    waitmovement(0)
    msgbox("Grunt 2: Uh… Shook?\nWhat should we do with them?")
    applymovement(ALLARD, mFaceDown)
    waitmovement(0)
    msgbox(format("Shook: Balfour will be angry if we let them through…\pI guess we have to take them out, ALLARD."))
    msgbox("Allard: Get ready, kid!\nWe're both coming at you at once!")
    closemessage
    delay(15)
    msgbox("???: I don't think so!")
    playse(SE_PIN)
    applymovement(SHOOK, mLookUpStartled)
    applymovement(ALLARD, mLookUpStartled)
    waitmovement(0)
    closemessage
    addobject(BRADLEY)
    getplayerxy(VAR_X_TEMP, VAR_Y_TEMP)
    if(var(VAR_X_TEMP) == 20) {
        // Player is in front of Shook
        applymovement(BRADLEY, mBradleyApproachesAllard)
    }
    else {
        // Player is in front of Allard
        applymovement(BRADLEY, mBradleyApproachesShook)
    }
    waitmovement(0)
    msgbox(format("Allard: How did you get through the forest!?"))
    msgbox(format("Bradley: I broke through everything in my way!\pYou Rockets are no match for me!"))
    closemessage
    applymovement(BRADLEY, mBradleyFidgetsDown)
    waitmovement(0)
    msgbox(format("Bradley: Nice to see you, {PLAYER}!\nAre you ready?\pThese guys are no match for us together!"))
    closemessage
    delay(30)
    applymovement(SHOOK, mFaceRight)
    applymovement(ALLARD, mFaceLeft)
    waitmovement(0)
    msgbox(format("Allard: This is bad, Shook!\pI don't think we can beat both of them…"))
    msgbox(format("Shook: We have to try…\pWe can't get an executive angry, not on our first mission."))
    if(var(VAR_X_TEMP) == 20) {
        // Player is in front of Shook
        applymovement(SHOOK, mFaceDown)
        applymovement(ALLARD, mFaceUp)
    }
    else {
        // Player is in front of Allard
        applymovement(SHOOK, mFaceUp)
        applymovement(ALLARD, mFaceDown)
    }
    closemessage
    // TODO battle vs shook and allard (team double battle)
    copyobjectxytoperm(BRADLEY)
    copyobjectxytoperm(SHOOK)
    copyobjectxytoperm(ALLARD)
    setrespawn(HEAL_LOCATION_CRANEWING_CITY)
    trainerbattle_double(TRAINER_SHOOK_AND_ALLARD_1, Route204_Text_ShookAndAllardIntro, Route204_Text_ShookAndAllardDefeated, Route204_Text_ShookAndAllardNoMons, Route204_ShookAndAllardDefeated, NO_MUSIC)
}

script(global) Route204_ShookAndAllardDefeated {
    msgbox("Allard: What do we do now?")
    msgbox("Shook: … … …\nWe have to run this time.\pBut we'll see you again, {PLAYER}.")
    closemessage
    if(var(VAR_X_TEMP) == 20) {
        // Player is in front of Shook
        applymovement(SHOOK, mShookOrAllardRunAway)
        applymovement(ALLARD, mAllardRunAway)
    }
    else {
        // Player is in front of Allard
        applymovement(SHOOK, mShookRunAway)
        applymovement(ALLARD, mShookOrAllardRunAway)
    }
    waitmovement(0)
    removeobject(SHOOK)
    removeobject(ALLARD)
    msgbox(Route204_Text_BradleyIsHurt)
    closemessage
    applymovement(BRADLEY, mFaceUp)
    delay(60)
    msgbox(Route204_Text_RocketsRipUpPlants)
    closemessage
    applymovement(BRADLEY, mBradleyFidgetsDown)
    waitmovement(0)
    msgbox(Route204_Text_BradleyDontWorryAboutMe)
    setflag(FLAG_ROCKETS_IN_CHARTREE)
    clearflag(FLAG_HIDE_ROCKETS_IN_CHARTREE)
    setvar(VAR_ROUTE204_STATE, ROUTE204_STATE_DEFEATED_SHOOK_ALLARD)
    setvar(VAR_CHARTREE_FOREST_STATE, CHARTREE_FOREST_ROCKETS_IN_FOREST)
    setflag(FLAG_HIDE_SHOOK_ALLARD_ROUTE_204)
    closemessage
    release
    end
}

script(global) Route204_BradleyHurt {
    msgbox(format("Bradley: I'll get someone to help me to the Pokémon Center.\pYou need to hurry!"), MSGBOX_NPC)
    end
}

//=====================================
// Local Scripts
//=====================================

//=====================================
// Text
//=====================================
text Route204_Text_AinaIntro {
    format(
        "Did you know the ash in the forest can be used in makeup, as well as glass?"
    )
}

text Route204_Text_AinaDefeated {
    format(
        "That's okay.\nPokémon battles are only a hobby."
    )
}

text Route204_Text_AinaPostBattle {
    format(
        "I'm thinking about starting a new line of beauty products made from ash!\pI'm trying to come up with a name…"
    )
}

text Route204_Text_DerrickIntro {
    format(
        "I'm not hiding from all of the terrifying bugs in the forest.\pNope!\nNot me!\pJust watch this!"
    )
}

text Route204_Text_DerrickDefeated {
    format(
        "Okay, okay. M-maybe I'm a little scared."
    )
}

text Route204_Text_DerrickPostBattle {
    format(
        "Don't tell anyone you saw me back here, okay?\pMy dad would be furious."
    )
}

text Route204_Text_LexiIntro {
    format(
        "The air out here is so refreshing!\pIt's perfect for a morning run to get the blood flowing.\pYou know what else gets the blood flowing?\pThat's right, a Pokémon battle!"
    )
}

text Route204_Text_LexiDefeated {
    format(
        "Now that was exhilerating!"
    )
}

text Route204_Text_LexiPostBattle {
    format(
        "I like to take my Pokémon running with me.\pI wonder if the exercise helps them get stronger, too?"
    )
}

text Route204_Text_BeatriceIntro {
    format(
        "I've been training Pokémon since before you were born!\pIf you get past me, you'll be in Cranewing.\pThink you can handle me?"
    )
}

text Route204_Text_BeatriceDefeated {
    format(
        "Ohoho!\nYou certainly are capable!"
    )
}

text Route204_Text_BeatricePostBattle {
    format(
        "My hobby is training Pokémon.\pWhenever one of mine gets strong, I trade for a weaker, more rare Pokémon.\pThat way I can do the whole thing again!"
    )
}

text Route204_Text_BradleyIsHurt {
    format(
        "Bradley: I put on a show for them, but I hurt myself, {PLAYER}.\p"
        "I've done something to my ankle, and I don't think I can battle anymore.\p"
        "I beat a couple of the guys in there, but there are still a few left."
    )
}

text Route204_Text_RocketsRipUpPlants {
    format(
        "Bradley: They're collecting the ash by ripping all the plants "
        "with ash on them out of the ground…\p"
        "If they do that…\nwhere will all the Pokémon live?"
    )
}

text Route204_Text_BradleyDontWorryAboutMe {
    format(
        "Bradley: {PLAYER}, you have to go in there and stop them!\p"
        "Don't worry about me, I'll get someone to help me to the Pokémon Center.\p"
        "You need to hurry!"
    )
}

text Route204_Text_ShookAndAllardIntro {
    format(
        "Shook: Here we come!"
    )
}

text Route204_Text_ShookAndAllardDefeated {
    format(
        "Allard: Not good, not good!"
    )
}

text Route204_Text_ShookAndAllardNoMons {
    format(
        "placeholder no mons"
    )
}


//=====================================
// Movement
//=====================================

movement mShookAndAllardApproach1 {
    walk_down * 4
}

movement mShookAndAllardApproach2 {
    walk_down * 3
}

movement mBradleyApproachesShook {
    walk_down * 6
}

movement mBradleyApproachesAllard {
    walk_right
    walk_down * 6
}

movement mShookOrAllardRunAway {
    walk_up * 7
    set_invisible
}

movement mShookRunAway {
    walk_right
    walk_fast_up * 7
    set_invisible
}

movement mAllardRunAway {
    walk_left
    walk_fast_up * 7
    set_invisible
}

//=====================================
// Raw
//=====================================
