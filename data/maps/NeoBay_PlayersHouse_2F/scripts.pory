const PLAYER = OBJ_EVENT_ID_PLAYER

//=====================================
// Map Scripts
//=====================================
mapscripts NeoBay_PlayersHouse_2F_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE[
        FLAG_SAW_JIRACHI_PROGRAM, 0: PlayersHouse_WatchJirachiProgram
    ]
}


//=====================================
// Global Scripts
//=====================================

script(global) NeoBay_PlayersHouse_2F_PC {
	lockall
	setvar(VAR_0x8004, PC_LOCATION_PLAYERS_HOUSE)
	special(DoPCTurnOnEffect)
	playse(SE_PC_ON)
	msgbox(gText_PlayerHouseBootPC, MSGBOX_DEFAULT)
	special(BedroomPC)
	waitstate
	releaseall
	end
}

script(global) NeoBay_PlayersHouse_2F_TurnOffPlayerPC {
	setvar(VAR_0x8004, PC_LOCATION_PLAYERS_HOUSE)
	playse(SE_PC_OFF)
	special(DoPCTurnOffEffect)
	releaseall
	end
}

script(global) NeoBay_PlayersHouse_2F_Notebook {
    msgbox(NeoBay_PlayersHouse_2F_Text_Notebook, MSGBOX_SIGN)
    end
}

script(global) NeoBay_PlayersHouse_2F_GameCube {
    msgbox(NeoBay_PlayersHouse_2F_Text_GameCube, MSGBOX_SIGN)
    end
}

script(global) NeoBay_PlayersHouse_2F_Drawer {
    msgbox(NeoBay_PlayersHouse_2F_Text_Drawer, MSGBOX_SIGN)
    end
}

script(global) NeoBay_PlayersHouse_WallClock {
	if(flag(FLAG_SET_WALL_CLOCK)) {
        goto(NeoBay_PlayersHouse_CheckWallClock)
    }
	msgbox(NeoBay_PlayersHouse_2F_ClockStopped, MSGBOX_DEFAULT)
	fadescreen(FADE_TO_BLACK)
	special(StartWallClock)
	waitstate
	delay(30)
	setflag(FLAG_SET_WALL_CLOCK)
	releaseall
	end
}

script(global) NeoBay_PlayersHouse_CheckWallClock {
	incrementgamestat(GAME_STAT_CHECKED_CLOCK)
	fadescreen(FADE_TO_BLACK)
	special(Special_ViewWallClock)
	waitstate
	releaseall
	end
}

//=====================================
// Local Scripts
//=====================================
script(local) PlayersHouse_WatchJirachiProgram {
    lockall
    setflag(FLAG_SAW_JIRACHI_PROGRAM)
    special(TurnOnTVScreen)
    delay(64)
    playse(SE_PIN)
    applymovement(PLAYER, mLookUpStartled)
    waitmovement(0)
    applymovement(PLAYER, mGoWatchJirachiProgram)
    waitmovement(0)
    msgbox(PlayersHouse_2F_JirachiProgram)
    waitmessage
    closemessage
    releaseall
    return
}

//=====================================
// Text
//=====================================
text PlayersHouse_2F_JirachiProgram {
    format(
        "The day foretold in the ancient legend is almost here!\p"
        "The 1000-year celebration marking the awakening of Jirachi!\p"
        "Music, food, and entertainment galore, featuring a live "
        "performance by\lDJ Dane!\pDon't miss out on the festivities!…"
    )
}

text NeoBay_PlayersHouse_2F_Text_Notebook {
    format (
        "{PLAYER} flipped open the notebook.\p"
        "ADVENTURE RULE NO. 1\n"
        "Open the MENU with START.\p"
        "ADVENTURE RULE NO. 2\n"
        "Record your progress with SAVE.\p"
        "The remaining pages are blank…"
    )
}

text NeoBay_PlayersHouse_2F_Text_GameCube {
    format (
	    "It's a Nintendo GameCube.\p"
	    "A Game Boy Advance is connected to\n"
	    "serve as the Controller.$"
    )
}

text NeoBay_PlayersHouse_2F_Text_Drawer {
    format (
        "{PLAYER} pulled open the drawer.\p"
        "The clothes inside are in complete disarray."
    )
}

text NeoBay_PlayersHouse_2F_ClockStopped {
	"The clock is stopped…\p"
	"Better set it and start it!$"
}

//=====================================
// Movement
//=====================================
movement mGoWatchJirachiProgram {
    delay_16 * 4
    walk_up * 2
}

//=====================================
// Raw
//=====================================
