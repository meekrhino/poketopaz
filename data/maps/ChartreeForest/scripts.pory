const PLAYER = OBJ_EVENT_ID_PLAYER
const FROY = 1
const GRUNT = 14
const BALFOUR = 15

// VAR_CHARTREE_FOREST_STATE values
const CHARTREE_FOREST_INIT = 0
const CHARTREE_FOREST_MET_FROY = 1
const CHARTREE_FOREST_ROCKETS_IN_FOREST = 2
const CHARTREE_FOREST_ROCKETS_DEFEATED = 3

// VAR_SOFTWIND_CITY_STATE values
const SOFTWIND_STATE_INIT = 0
const SOFTWIND_STATE_MET_ALISA = 1
const SOFTWIND_STATE_TEAM_ENCOUNTERED = 2
const SOFTWIND_STATE_DEFEATED_ROCKETS = 3
const SOFTWIND_STATE_MET_DEFENDERS = 4

// VAR_CRANEWING_CITY_STATE values
const CRANEWING_STATE_INIT = 0
const CRANEWING_STATE_SAW_GLASSWORKS = 1
const CRANEWING_STATE_MET_BALFOUR = 2
const CRANEWING_STATE_SAW_BRADLEY_AT_CENTER = 3

const VAR_TEMP_X = VAR_TEMP_0
const VAR_TEMP_Y = VAR_TEMP_1

//=====================================
// Map Scripts
//=====================================
mapscripts ChartreeForest_MapScripts {
    MAP_SCRIPT_ON_RESUME: ChartreeForest_OnResume
}

//=====================================
// Global Scripts
//=====================================
script(global) ChartreeForest_TriggerFroy {
    lockall
    turnobject(PLAYER, DIR_EAST)
    getplayerxy(VAR_TEMP_X, VAR_TEMP_Y)
    switch(var(VAR_TEMP_Y)) {
        case 20:
            applymovement(FROY, mFroyApproaches_1)
        case 21:
            applymovement(FROY, mFroyApproaches_2)
        case 22:
            applymovement(FROY, mFroyApproaches_3)
    }
    waitmovement(0)
    msgbox(ChartreeForest_Text_FroyIntroduction)
    waitmessage
    closemessage
    applymovement(FROY, mFroyLooksAround)
    waitmovement(0)
    msgbox(ChartreeForest_Text_FroyAboutGym)
    waitmessage
    closemessage
    applymovement(FROY, mFroyStartsToLeave)
    waitmovement(0)
    msgbox(ChartreeForest_Text_FroyAboutAsh)
    waitmessage
    closemessage
    quest_start(QUEST_CHARTREE_ROCKETS)
    switch(var(VAR_TEMP_Y)) {
        case 20:
            applymovement(FROY, mFroyLeaves_1)
        case 21:
            applymovement(FROY, mFroyLeaves_2)
        case 22:
            applymovement(FROY, mFroyLeaves_3)
    }
    waitmovement(0)
    removeobject(FROY)
    setvar(VAR_CHARTREE_FOREST_STATE, CHARTREE_FOREST_MET_FROY)
    releaseall
    end
}

script(global) ChartreeForest_SignRoute203 {
    msgbox("{DOWN_ARROW} Exit to Route 203", MSGBOX_SIGN)
    end
}

script(global) ChartreeForest_SignRoute204 {
    msgbox("{DOWN_ARROW} Exit to Route 204", MSGBOX_SIGN)
    end
}

script(global) ChartreeForest_Sign {
    msgbox(format("Chartree Forest\p“In memory of Pinedale Forest, which once stood here”"), MSGBOX_SIGN)
    end
}

script(global) ChartreeForest_DeadendMan {
    msgbox(format("Oh man, I must have taken a wrong turn.\pI really wish I was going the other direction!"), MSGBOX_NPC)
    end
}

script(global) ChartreeForest_LostWoman {
    msgbox(format("I'm so lost…\pI jumped over that ledge and now I don't know where to go…"), MSGBOX_NPC)
    end
}

script(global) ChartreeForest_Gentleman {
    trainerbattle_single(TRAINER_FRANKLIN, ChartreeForest_Text_FranklinIntro, ChartreeForest_Text_FranklinDefeated)
    msgbox(ChartreeForest_Text_FranklinPostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) ChartreeForest_BugCatcher {
    trainerbattle_single(TRAINER_MARVIN, ChartreeForest_Text_MarvinIntro, ChartreeForest_Text_MarvinDefeated)
    msgbox(ChartreeForest_Text_MarvinPostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) ChartreeForest_Fisherman {
    trainerbattle_single(TRAINER_WALLY, ChartreeForest_Text_WallyIntro, ChartreeForest_Text_WallyDefeated)
    msgbox(ChartreeForest_Text_WallyPostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) ChartreeForest_HexManiac {
    trainerbattle_single(TRAINER_ZELDA, ChartreeForest_Text_ZeldaIntro, ChartreeForest_Text_ZeldaDefeated)
    msgbox(ChartreeForest_Text_ZeldaPostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) ChartreeForest_Lass {
    trainerbattle_single(TRAINER_SARAH, ChartreeForest_Text_SarahIntro, ChartreeForest_Text_SarahDefeated)
    msgbox(ChartreeForest_Text_SarahPostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) ChartreeForest_RocketGrunt1 {
    trainerbattle_single(TRAINER_GRUNT_CHARTREE_1, ChartreeForest_Text_RocketGrunt1Intro, ChartreeForest_Text_RocketGrunt1Defeated)
    msgbox(ChartreeForest_Text_RocketGrunt1PostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) ChartreeForest_RocketGrunt2 {
    trainerbattle_single(TRAINER_GRUNT_CHARTREE_2, ChartreeForest_Text_RocketGrunt2Intro, ChartreeForest_Text_RocketGrunt2Defeated)
    msgbox(ChartreeForest_Text_RocketGrunt2PostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) ChartreeForest_RocketGrunt3 {
    trainerbattle_single(TRAINER_GRUNT_CHARTREE_3, ChartreeForest_Text_RocketGrunt3Intro, ChartreeForest_Text_RocketGrunt3Defeated)
    msgbox(ChartreeForest_Text_RocketGrunt3PostBattle, MSGBOX_AUTOCLOSE)
    end
}

script(global) ChartreeForest_RocketGrunt4 {
    msgbox(format("Sorry, kid.\nI was told not to leave this spot.\pI know better than to get drawn into a fight with one of you Trainer kids.\pGet gone!"), MSGBOX_NPC)
    end
}

script(global) ChartreeForest_Balfour {
    lock
    playse(SE_PIN)
    applymovement(GRUNT, mLookRightStartled)
    waitmovement(0)
    msgbox(format("Grunt: Uh, Balfour sir?\pThis kid just beat all the workers that were left."))
    closemessage
    getplayerxy(VAR_TEMP_X, VAR_TEMP_Y)
    switch(var(VAR_TEMP_Y)) {
        case 7:
            // Top spot
            applymovement(BALFOUR, mBalfourApproachesTop)
        case 8:
            // Bottom spot
            applymovement(BALFOUR, mBalfourApproachesBottom)
    }
    waitmovement(0)
    msgbox(ChartreeForest_Text_BalfourAttacks)
    closemessage
    applymovement(BALFOUR, mWalkRight2)
    waitmovement(0)
    msgbox(format("Balfour: We're taking this ash, and there isn't anything you can do about it."))
    closemessage
    trainerbattle_no_intro(TRAINER_BALFOUR_1, ChartreeForest_Text_BalfourDefeated)
    msgbox("Grunt: …what?\pThis kid beat an Executive!?")
    msgbox(ChartreeForest_Text_BalfourScoffsAtDefeat)
    closemessage
    switch(var(VAR_TEMP_Y)) {
        case 7:
            // Top spot
            applymovement(BALFOUR, mBalfourDepartsTop)
        case 8:
            // Bottom spot
            applymovement(BALFOUR, mBalfourDepartsBottom)
    }
    waitmovement(0)
    msgbox(format("Grunt: Where are Allard and Shook?\pIf it wasn't for their failure, we could have collected so much more ash…"))
    closemessage
    applymovement(GRUNT, mGruntRunsAwayAfterBalfour)
    waitmovement(0)
    removeobject(BALFOUR)
    removeobject(GRUNT)
    setvar(VAR_CHARTREE_FOREST_STATE, CHARTREE_FOREST_ROCKETS_DEFEATED)
    setvar(VAR_CRANEWING_CITY_STATE, CRANEWING_STATE_MET_BALFOUR)
    setvar(VAR_SOFTWIND_CITY_STATE, SOFTWIND_STATE_DEFEATED_ROCKETS)
    setflag(FLAG_HIDE_BRADLEY_ROUTE_204)
    clearflag(FLAG_ROCKETS_IN_CHARTREE)
    setflag(FLAG_HIDE_ROCKETS_IN_CHARTREE)
    setflag(FLAG_THERMA_RETURNED) //hides GYM DUDE in softwindi
    clearflag(FLAG_HIDE_SOFTWIND_CITY_ORION)
    clearflag(FLAG_HIDE_SOFTWIND_CITY_WILLOW)
    clearflag(FLAG_HIDE_BRADLEY_CRANEWING_CENTER)
    subquest_finish(SUB_QUEST_DEFEAT_ROCKETS)
    quest_set_reward_waiting(QUEST_CHARTREE_ROCKETS)
    warp(MAP_CRANEWING_CITY_POKEMON_CENTER, 2, 0, 0)
    release
    end
}

//=====================================
// Local Scripts
//=====================================
script ChartreeForest_OnResume {
	setstepcallback(STEP_CB_ASH)
}

//=====================================
// Text
//=====================================
text ChartreeForest_Text_FroyIntroduction {
    format(
        "Froy: Hey kid, the name's Froy.\pHave you seen any "
        "people in black around?\pThey've stirred up all "
        "of the ash, and I have to wait for it to fully settle "
        "again before it can be collected.\p"
        "I'm not sure what they're looking for, but…"
    )
}

text ChartreeForest_Text_FroyAboutGym {
    format(
        "Froy: It looks like they've gone, at least for now.\p"
        "It's supposed to be my job as gym leader to protect "
        "the forest from harm, but if they've already gone, there "
        "isn't much I can do…\p"
        "Froy: Oh!\nYou're looking to challenge me?\p"
        "Well, we can't do it here.\nCranewing, my home, is just "
        "past here.\pIf you can make it through the rest of the "
        "forest yourself, you might have a chance of beating me!"
    )
}

text ChartreeForest_Text_FroyAboutAsh {
    format(
        "Froy: The ash here is important for making glass.\p"
        "When you reach Cranewing, you'll appreciate why we "
        "value this ash so highly.\pBut to the common person, "
        "there is nothing in the forest of worth.\pI wonder what "
        "those people in black were doing here…"
    )
}

text ChartreeForest_Text_FranklinIntro {
    format(
        "Ahem!\pI walked just a few steps into this forest, and got a lung full of air.\pAnd my clothes are filthy!\nHow irritating!"
    )
}

text ChartreeForest_Text_FranklinDefeated {
    format(
        "You can handle this forest, I'm sure."
    )
}

text ChartreeForest_Text_FranklinPostBattle {
    format(
        "I think I'll go home and clean myself up.\pI can't arrive in Cranewing looking like this."
    )
}

text ChartreeForest_Text_MarvinIntro {
    format(
        "I can't decide if this forest is great for bug trainers, or terrifying!"
    )
}

text ChartreeForest_Text_MarvinDefeated {
    format(
        "I guess I'm leaning toward terrifying if you're in here!"
    )
}

text ChartreeForest_Text_MarvinPostBattle {
    format(
        "There are some awesome bugs in this forest, but all these fire types make it hard for my other Pokémon."
    )
}

text ChartreeForest_Text_WallyIntro {
    format(
        "You must be wondering what a fisherman like me is doing in a dark, dusty place like this!"
    )
}

text ChartreeForest_Text_WallyDefeated {
    format(
        "I need more training still…"
    )
}

text ChartreeForest_Text_WallyPostBattle {
    format(
        "It's simple!\nI come here to train my Pokémon against all of these Fire type Pokémon!\pThere may not be any Water Pokémon to catch, but it's great for training then!"
    )
}

text ChartreeForest_Text_ZeldaIntro {
    format(
        "… … …\n…I've been waiting for you…"
    )
}

text ChartreeForest_Text_ZeldaDefeated {
    format(
        "… … …"
    )
}

text ChartreeForest_Text_ZeldaPostBattle {
    format(
        "…I knew you would defeat me…"
    )
}

text ChartreeForest_Text_SarahIntro {
    format(
        "You'd better turn back now!\pYou'll never get through this forest. You're not even close!"
    )
}

text ChartreeForest_Text_SarahDefeated {
    format(
        "You beat me, but you should still turn back!"
    )
}

text ChartreeForest_Text_SarahPostBattle {
    format(
        "…Okay, I might have exaggerated.\pYou're actually very close to the exit."
    )
}

text ChartreeForest_Text_RocketGrunt1Intro {
    format(
        "Team Rocket has important work in this forest.\pWe have no time for you.\nGet out of our way!"
    )
}

text ChartreeForest_Text_RocketGrunt1Defeated {
    format(
        "Another kid causing trouble…"
    )
}

text ChartreeForest_Text_RocketGrunt1PostBattle {
    format(
        "Well, you beat me.\pWhat, you thought I'd just let you pass?\pMy Pokémon are defeated, but I'm still standing."
    )
}

text ChartreeForest_Text_RocketGrunt2Intro {
    format(
        "What's a little kid like you doing in a dangerous place like this?"
    )
}

text ChartreeForest_Text_RocketGrunt2Defeated {
    format(
        "This strong at your age?"
    )
}

text ChartreeForest_Text_RocketGrunt2PostBattle {
    format(
        "You may be strong, but you'd still best step lightly.\pTeam Rocket has business here.\nThat means you don't."
    )
}

text ChartreeForest_Text_RocketGrunt3Intro {
    format(
        "You must be the kid that fool lost to.\pThat won't happen again!\nI won't disappoint Balfour!"
    )
}

text ChartreeForest_Text_RocketGrunt3Defeated {
    format(
        "Such shame…"
    )
}

text ChartreeForest_Text_RocketGrunt3PostBattle {
    format(
        "Look, just don't mention you saw me, alright?\pI'd rather Balfour just thinks I missed seeing you…"
    )
}

text ChartreeForest_Text_BalfourAttacks {
    format(
        "Balfour: …This isn't the same one that defeated all of your Pokémon, is it?\p"
        "This is someone new.\p"
        "Why can't any of our missions end peacefully?\p"
        "Why is it that we always end up fighting?\p"
        "Well, I guess it's part of the job, stopping ignorant children."
    )
}

text ChartreeForest_Text_BalfourDefeated {
    format(
        "… … …!\nJust luck, that's all."
    )
}

text ChartreeForest_Text_BalfourScoffsAtDefeat {
    format(
        "Balfour: Heh, you're lucky, kid.\p"
        "I didn't bring my own Pokémon into this sooty forest, but still…\p"
        "You are impressive.\nYou win this time, but I have no doubt we will meet again."
    )
}

//=====================================
// Movement
//=====================================
movement mFroyApproaches_1 {
    walk_up
    walk_left * 3
}

movement mFroyApproaches_2 {
    walk_left * 4
}

movement mFroyApproaches_3 {
    walk_down
    walk_left * 4
}

movement mFroyLooksAround {
    face_right
    delay_16 * 3
    face_up
    delay_16 * 3
    face_down
    delay_16 * 3
    face_right
    delay_16 * 3
    face_left
}

movement mFroyStartsToLeave {
    walk_right * 2
    face_left
}

movement mFroyLeaves_1 {
    walk_down
    walk_right * 4
    walk_down * 4
    walk_right * 2
    set_invisible
}

movement mFroyLeaves_2 {
    walk_right * 5
    walk_down * 4
    walk_right * 2
    set_invisible
}

movement mFroyLeaves_3 {
    walk_right * 5
    walk_down * 3
    walk_right * 2
    set_invisible
}

movement mLookRightStartled {
    face_right
    emote_exclamation_mark
}

movement mBalfourApproachesTop {
    walk_right * 3
}

movement mBalfourApproachesBottom {
    walk_right * 2
    walk_down
    walk_right
}

movement mWalkRight2 {
    walk_right * 2
}

movement mBalfourDepartsTop {
    walk_left * 8
    set_invisible
}

movement mBalfourDepartsBottom {
    walk_left * 2
    walk_up
    walk_left * 6
    set_invisible
}

movement mGruntRunsAwayAfterBalfour {
    walk_fast_left * 4
    set_invisible
}

//=====================================
// Raw
//=====================================
