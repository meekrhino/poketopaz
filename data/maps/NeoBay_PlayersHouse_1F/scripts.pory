const PLAYER = OBJ_EVENT_ID_PLAYER
const MOM = 1

// VAR_NEOBAY_STATE values
const NEOBAY_STATE_NEW_GAME = 0
const NEOBAY_STATE_SPOKE_TO_MOM = 1
const NEOBAY_STATE_MET_WILLOW = 2
const NEOBAY_STATE_RECEIVED_STARTER = 3
const NEOBAY_STATE_AMPURE_RESCUED = 4
const NEOBAY_STATE_STARTED_FLASHBACK = 5
const NEOBAY_STATE_SAW_FLASHBACK = 6
const NEOBAY_STATE_RECEIVED_RUNNING_SHOES = 7
const NEOBAY_STATE_DEPARTED = 8
const NEOBAY_STATE_OPTIONAL_RIVAL_DONE = 9

//=====================================
// Map Scripts
//=====================================
mapscripts NeoBay_PlayersHouse_1F_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: PlayersHouse_1F_OnTransition
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_NEOBAY_STATE, 0: BradleyWasLookingForYou
    ]
}

//=====================================
// Global Scripts
//=====================================
script(global) PlayersHouse_1F_BradleyReminder {
    switch(var(VAR_NEOBAY_STATE)) {
        case NEOBAY_STATE_SPOKE_TO_MOM:
            msgbox(format("Mom: Bradley was looking for you, dear.\nHe said you'd know where to find him."), MSGBOX_NPC)
        case NEOBAY_STATE_SAW_FLASHBACK:
            call(MomGivesRunningShoes)
        case NEOBAY_STATE_RECEIVED_RUNNING_SHOES:
            msgbox(format("Mom: I'll be fine. Here, why not have a quick rest?"), MSGBOX_NPC)
            call(Common_EventScript_OutOfCenterPartyHeal)
        case NEOBAY_STATE_DEPARTED:
            msgbox(format("Mom: Welcome home, dear!\nI think I saw Bradley come back, did you see him?\pBefore you go, why not have a quick rest?"), MSGBOX_NPC)
            call(Common_EventScript_OutOfCenterPartyHeal)
        case NEOBAY_STATE_OPTIONAL_RIVAL_DONE:
            msgbox(format("Mom: How nice to see you!\nI hope your adventure is going well.\pWhy not have a quick rest?"), MSGBOX_NPC)
            call(Common_EventScript_OutOfCenterPartyHeal)
    }
    end
}

//=====================================
// Local Scripts
//=====================================
script(local) PlayersHouse_1F_OnTransition {
	setrespawn(HEAL_LOCATION_NEO_BAY)
    switch(var(VAR_NEOBAY_STATE)) {
        case NEOBAY_STATE_SAW_FLASHBACK:
            setobjectxyperm(MOM, 2, 2)
            setobjectmovementtype(MOM, MOVEMENT_TYPE_FACE_UP)
        case NEOBAY_STATE_RECEIVED_RUNNING_SHOES:
            setobjectxyperm(MOM, 3, 4)
            setobjectmovementtype(MOM, MOVEMENT_TYPE_FACE_RIGHT)
    }
    end
}

script(local) BradleyWasLookingForYou {
    lockall
    setvar(VAR_NEOBAY_STATE, NEOBAY_STATE_SPOKE_TO_MOM)
    playse(SE_PIN)
    applymovement(MOM, mLookUpStartled)
    waitmovement(0)
    delay(48)
    applymovement(PLAYER, mPlayerWalksOverToMom)
    waitmovement(0)
    msgbox(PlayersHouse_1F_BradleyWasLookingForYou, MSGBOX_AUTOCLOSE)
    releaseall
    end
}

script(local) MomGivesRunningShoes {
    lockall
    faceplayer
    setvar(VAR_NEOBAY_STATE, NEOBAY_STATE_RECEIVED_RUNNING_SHOES)
    msgbox(format("Mom: Hi {PLAYER}. Where have you been all day?\p… … …\n… … …\l… … …\pWow, that sounds like an adventure.\nBut I'm not sure I like the idea of you\lleaving…"))
    waitmessage
    closemessage
    applymovement(MOM, mMomDeliberates1)
    waitmovement(MOM)
    msgbox(format("Mom: It's dangerous out there, {PLAYER}.\pI don't think you have any idea…"))
    waitmessage
    closemessage
    applymovement(MOM, mMomDeliberates2)
    waitmovement(MOM)
    msgbox(PlayersHouse_1F_GivesRunningShoes)
    waitmessage
    closemessage
	playfanfare(MUS_OBTAIN_ITEM)
	message("{PLAYER} put on the\nRunning Shoes.")
	waitfanfare
	setflag(FLAG_SYS_B_DASH)
    msgbox(PlayersHouse_1F_SayGoodbye)
    waitmessage
    closemessage
    releaseall
    return
}

//=====================================
// Text
//=====================================
text PlayersHouse_1F_BradleyWasLookingForYou {
    format(
        "Mom: Oh {PLAYER}!\nBradley just came by looking for you.\p"
        "He said it was urgent, but I sent him "
        "away because you were still asleep.\p"
        "You should go find him, he was very excited.\p"
        "Well, even more excited than he usually is."
    )
}

text PlayersHouse_1F_GivesRunningShoes {
    format(
        "Mom: I understand that you want to find Simon, but there's no "
        "telling where he could be now.\p"
        "… … …\n… … …\l… … …\l… … …\p"
        "I guess I've held you back for long enough.\p"
        "You'll be with your Pokémon.\n"
        "That should be enough.\p"
        "But take these with you, {PLAYER}, and make sure you use them."
    )
}

text PlayersHouse_1F_SayGoodbye {
    format(
        "Mom: If you're ever in danger, or you need to come home, "
        "these running shoes will help you move much faster.\p"
        "I'll miss you, {PLAYER}.\pMake sure you come home once in "
        "a while."
    )
}

//=====================================
// Movement
//=====================================
movement mMomDeliberates1 {
    walk_slow_left * 2
    face_up
    delay_16
    walk_in_place_slow_up * 2
    delay_16
    walk_slow_right
    face_up
    walk_in_place_slow_up * 2
    delay_16 * 2
    face_left
}

movement mMomDeliberates2 {
    delay_16 * 4
    walk_right
    face_down
}

movement mPlayerWalksOverToMom {
    walk_left * 2
    walk_down * 2
}

//=====================================
// Raw
//=====================================
