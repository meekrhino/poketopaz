const PLAYER = OBJ_EVENT_ID_PLAYER
const CAMERA = OBJ_EVENT_ID_CAMERA
const GYM_DUDE = 7
const WILLOW = 8
const ALISA = 9
const ROCKET = 10
const DEFENDER = 11

const VAR_TEMP_X = VAR_TEMP_0
const VAR_TEMP_Y = VAR_TEMP_1

// VAR_SOFTWIND_CITY_STATE values
const SOFTWIND_STATE_INIT = 0
const SOFTWIND_STATE_MET_ALISA = 1
const SOFTWIND_STATE_TEAM_ENCOUNTERED = 2
const SOFTWIND_STATE_DEFEATED_ROCKETS = 3
const SOFTWIND_STATE_MET_DEFENDERS = 4
const SOFTWIND_STATE_DEFEATED_THERMA = 5
const SOFTWIND_STATE_ALISA_AND_WILLOW = 6
const SOFTWIND_STATE_DEFEATED_ALISA = 7

//=====================================
// Map Scripts
//=====================================
mapscripts SoftwindCity_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: SoftwindCity_OnTransition
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_SOFTWIND_CITY_STATE, SOFTWIND_STATE_DEFEATED_THERMA: SoftwindCity_AfterTherma
    ]
}

//=====================================
// Global Scripts
//=====================================
script(global) SoftwindCity_TownSign {
    msgbox(SoftwindCity_Text_TownSign, MSGBOX_SIGN)
    end
}

script(global) SoftwindCity_GymSign {
    msgbox(SoftwindCity_Text_GymSign, MSGBOX_SIGN)
    end
}

script(global) SoftwindCity_MoltresSign {
    msgbox(SoftwindCity_Text_TributeMoltres, MSGBOX_SIGN)
    end
}

script(global) SoftwindCity_ArticunoSign {
    msgbox(SoftwindCity_Text_TributeArticuno, MSGBOX_SIGN)
    end
}

script(global) SoftwindCity_ZapdosSign {
    msgbox(SoftwindCity_Text_TributeZapdos, MSGBOX_SIGN)
    end
}

script(global) SoftwindCity_ElderGuard {
    msgbox(format("My sisters and I hold the sacred duty of guarding the towers.\pUntil our protectors return, these doors shall remain closed."), MSGBOX_NPC)
    end
}

script(global) SoftwindCity_WomanInFlowers {
    quest_check_started(QUEST_MOOCALF_ORPHAN)
    if(var(VAR_RESULT) == TRUE) {
        subquest_check_finished(SUB_QUEST_FIND_MILK)
        if(var(VAR_RESULT) == FALSE) {
            msgbox(format("Hmm…\nMilk…\pNo, I don't know anywhere you could find that."), MSGBOX_NPC)
            end
        }
    }
    msgbox(format("I love the way the flowers sway in the breeze.\pI sit and watch them to help center myself."), MSGBOX_NPC)
    end
}

script(global) SoftwindCity_GirlHiding {
    msgbox(format("Wow, you noticed me hiding behind here?\pIt's so easy to hide behind buildings.\pI've actually stumbled on some lost and forgotten things a few times!"), MSGBOX_NPC)
    end
}

script(global) SoftwindCity_GuyInFrontOfGym {
    quest_check_started(QUEST_MOOCALF_ORPHAN)
    if(var(VAR_RESULT) == TRUE) {
        subquest_check_finished(SUB_QUEST_FIND_MILK)
        if(var(VAR_RESULT) == FALSE) {
            msgbox(format("I'm working here.\pGo ask someone else about milk."), MSGBOX_NPC)
            end
        }
    }
    msgbox(format("I'm sorry, Gym Leader Therma is currently away on research.\pShe will take your challenge when she returns."), MSGBOX_NPC)
    end
}

script(global) SoftwindCity_BaldingMan {
    quest_check_started(QUEST_MOOCALF_ORPHAN)
    if(var(VAR_RESULT) == TRUE) {
        subquest_check_finished(SUB_QUEST_FIND_MILK)
        if(var(VAR_RESULT) == FALSE) {
            msgbox(format("A sick Moocalf out at the old Farmhouse?\pI wish I could help you, but I don't have any milk."), MSGBOX_NPC)
            end
        }
    }
    msgbox(format("I'm old enough to remember Pinedale Forest before the fire.\pNow…\pWell, let's just say this.\pIf you're heading east, I hope you have Burn Heals!"), MSGBOX_NPC)
    end
}

script(global) SoftwindCity_TriggerAlisaIntro {
    lockall
    turnobject(PLAYER, DIR_NORTH)
    getplayerxy(VAR_TEMP_X, VAR_TEMP_Y)
    switch(var(VAR_TEMP_X)) {
        case 18:
            applymovement(ALISA, mAlisaApproaches_1)
        case 16:
            applymovement(ALISA, mAlisaApproaches_2)
        case 15:
            applymovement(ALISA, mAlisaApproaches_3)
        case 14:
            applymovement(ALISA, mAlisaApproaches_4)
        case 13:
            applymovement(ALISA, mAlisaApproaches_5)
        case 12:
            applymovement(ALISA, mAlisaApproaches_6)
    }
    waitmovement(0)
    msgbox(format("???: Hi!\nWho are you?\l… …\pWell, {PLAYER}, I guess you aren't so unfortunate.\pThere are certainly worse names out there."))
    waitmessage
    closemessage
    applymovement(ALISA, Common_Movement_FaceUp)
    waitmovement(0)
    delay(60)
    msgbox(format("???: This is Softwind City, and I've been waiting ages for the Gym Leader to get back…"))
    waitmessage
    closemessage
    delay(30)
    applymovement(ALISA, Common_Movement_FaceDown)
    waitmovement(0)
    delay(30)
    msgbox(SoftwindCity_Text_AlisaAboutTherma_1)
    waitmessage
    closemessage
	playfanfare(MUS_OBTAIN_ITEM)
	message(SoftwindCity_Text_ReceivedPokenav)
	waitfanfare
	setflag(FLAG_SYS_POKENAV_GET)
	setflag(FLAG_RECEIVED_POKENAV)
    msgbox(SoftwindCity_Text_AlisaAboutTherma_2)
    waitmessage
    closemessage
    applymovement(ALISA, mAlisaStartsToLeave)
    waitmovement(0)
    delay(60)
    turnobject(ALISA, DIR_SOUTH)
    delay(30)
    msgbox(format("???: What?\nMy name? It's Alisa.\pYou'd better remember that, {PLAYER}, 'cause I'm going to beat you to the Pokémon League.\pSo you'd better watch out, kid!"))
    waitmessage
    closemessage
    applymovement(ALISA, mAlisaFinishesLeaving)
    waitmovement(0)
    setflag(FLAG_HIDE_SOFTWIND_CITY_ALISA)
    setvar(VAR_SOFTWIND_CITY_STATE, SOFTWIND_STATE_MET_ALISA)
    removeobject(ALISA)
    releaseall
    end
}

script SoftwindCity_TriggerOrion_1 {
    lockall
    special(SpawnCameraObject)
    applymovement(CAMERA, mShiftCameraToRight)
    waitmovement(0)
    addobject(ROCKET)
    hideobjectat(ROCKET, MAP_SOFTWIND_CITY)
    addobject(DEFENDER)
    getplayerxy(VAR_TEMP_X, VAR_TEMP_Y)
    if(var(VAR_TEMP_Y) == 36) {
        setobjectxy(ROCKET, 45, 35)
        setobjectxy(DEFENDER, 45, 35)
    }
    else {
        setobjectxy(ROCKET, 45, 36)
        setobjectxy(DEFENDER, 45, 36)
    }
    applymovement(DEFENDER, mDefenderRunsIntoTown)
    delay(32)
    applymovement(ROCKET, mRocketFollows)
    waitmovement(0)
    msgbox(format("???: You had better run!\pAnd don't you dare come back around here again!"))
    waitmessage
    closemessage
    applymovement(ROCKET, mRocketReturnsToForest)
    waitmovement(0)
    removeobject(ROCKET)
    applymovement(CAMERA, mShiftCameraToLeft)
    waitmovement(0)
    msgbox(SoftwindCity_Text_DefenderHopesNotSeen)
    waitmessage
    closemessage
    turnobject(DEFENDER, DIR_WEST)
    playse(SE_PIN)
    applymovement(DEFENDER, mExclamationPoint)
    waitmovement(0)
    delay(48)
    switch(var(VAR_TEMP_Y)) {
        case 34:
            applymovement(DEFENDER, mDefenderAprroachesPlayer_1)
        case 35:
            applymovement(DEFENDER, mDefenderAprroachesPlayer_2)
        case 36:
            applymovement(DEFENDER, mDefenderAprroachesPlayer_3)
        case 37:
            applymovement(DEFENDER, mDefenderAprroachesPlayer_3)
    }
    waitmovement(0)
    msgbox("Hey you!\pForget you saw me here, okay?\nOtherwise…")
    waitmessage
    closemessage
    if(var(VAR_TEMP_Y) == 37) {
        applymovement(DEFENDER, mDefenderWalksPastPlayer_2)
    }
    else {
        applymovement(DEFENDER, mDefenderWalksPastPlayer_1)
    }
    delay(32)
    applymovement(PLAYER, mFaceDown)
    delay(48)
    applymovement(PLAYER, mFaceLeft)
    waitmovement(0)
    removeobject(DEFENDER)
    setflag(FLAG_HIDE_SOFTWIND_CITY_ROCKET)
    setflag(FLAG_HIDE_SOFTWIND_CITY_ORION)
    special(RemoveCameraObject)
    setvar(VAR_SOFTWIND_CITY_STATE, SOFTWIND_STATE_TEAM_ENCOUNTERED)
    releaseall
    end
}

script SoftwindCity_TriggerOrion_2 {
    lockall
    playse(SE_PIN)
    applymovement(DEFENDER, mStartledWalkRight)
    waitmovement(0)
    msgbox(SoftwindCity_Text_DefenderScolds)
    closemessage
    getplayerxy(VAR_TEMP_X, VAR_TEMP_Y)
    switch(var(VAR_TEMP_Y)) {
        case 34:
            applymovement(DEFENDER, mDefenderAprroachesPlayer_2_1)
        case 35:
            applymovement(DEFENDER, mDefenderAprroachesPlayer_2_2)
        case 36:
            applymovement(DEFENDER, mDefenderAprroachesPlayer_2_3)
        case 37:
            applymovement(DEFENDER, mDefenderAprroachesPlayer_2_4)
    }
    waitmovement(0)
    trainerbattle_no_intro(TRAINER_WOLSEY_1, SoftwindCity_Text_WolseyDefeated)
    msgbox(format("Defender: I don't understand what happened…\pHow could I lose to someone so inexperienced!?"))
    msgbox(format("Willow: {PLAYER}?\nIs that you?"))
    closemessage
    applymovement(DEFENDER, mFaceLeft)
    applymovement(WILLOW, mWillowApproachesDefender)
    waitmovement(0)
    msgbox("Defender: Uh oh.\pI've stayed too long.")
    closemessage
    delay(15)
    applymovement(DEFENDER, mFaceRight)
    delay(15)
    msgbox(SoftwindCity_Text_WolseyWillPunish)
    closemessage
    switch(var(VAR_TEMP_Y)) {
        case 34:
            applymovement(DEFENDER, mDefenderRunsOff)
        case 35:
            applymovement(DEFENDER, mDefenderRunsOff2)
        case 36:
            applymovement(DEFENDER, mDefenderRunsOff)
        case 37:
            applymovement(DEFENDER, mDefenderRunsOff)
    }
    waitmovement(0)
    removeobject(DEFENDER)
    switch(var(VAR_TEMP_Y)) {
        case 34:
            applymovement(WILLOW, mWillowApproachesPlayer1)
        case 35:
            applymovement(WILLOW, mWillowApproachesPlayer2)
        case 36:
            applymovement(WILLOW, mWillowApproachesPlayer3)
        case 37:
            applymovement(WILLOW, mWillowApproachesPlayer4)
    }
    waitmovement(0)
    msgbox(SoftwindCity_Text_WillowCatchesUp)
    opendoor(29, 32)
    waitdooranim
    addobject(GYM_DUDE)
    setobjectxy(GYM_DUDE, 29, 32)
    applymovement(GYM_DUDE, mWalkDown)
    closedoor(29, 32)
    waitdooranim
    delay(48)
    playse(SE_PIN)
    applymovement(GYM_DUDE, mExclamationPoint)
    waitmovement(0)
    msgbox("Man: Professor Willow!")
    closemessage
    applymovement(WILLOW, mFaceLeft)
    switch(var(VAR_TEMP_Y)) {
        case 34:
            applymovement(GYM_DUDE, mGymDudesApproaches1)
        case 35:
            applymovement(GYM_DUDE, mGymDudesApproaches2)
        case 36:
            applymovement(GYM_DUDE, mGymDudesApproaches3)
        case 37:
            applymovement(GYM_DUDE, mGymDudesApproaches4)
    }
    waitmovement(0)
    msgbox(SoftwindCity_Text_GymDude)
    msgbox("Willow: Thank you very much.")
    closemessage
    applymovement(GYM_DUDE, mGymDudeRushesOff)
    waitmovement(0)
    removeobject(GYM_DUDE)
    setflag(FLAG_THERMA_RETURNED) //redundant, but set for clarity
    applymovement(WILLOW, mFaceRight)
    waitmovement(0)
    msgbox(format("Willow: Well, {PLAYER}, Therma awaits!\pAfter beating Team Rocket, I have no doubt you'll win! Ohohoho!"))
    closemessage
    applymovement(WILLOW, mWillowDeparts)
    waitmovement(0)
    removeobject(WILLOW)
    setvar(VAR_SOFTWIND_CITY_STATE, SOFTWIND_STATE_MET_DEFENDERS)
    releaseall
    end
}

script(global) SoftwindCity_WillowWaitingByGym {
    msgbox(format("I'm certain you can beat her.\pShe's not half the prodigy she sees herself as."), MSGBOX_NPC)
    end
}

script(global) SoftwindCity_AlisaWaitingByGym {
    msgbox(format("Well then, {PLAYER}.\pWhy not show me how “special” you are?"), MSGBOX_NPC)
    switch(var(VAR_STARTER_MON)) {
        case 0:
            trainerbattle_no_intro(TRAINER_ALISA_1_VENAP, SoftwindCity_Text_AlisaDefeated2)
        case 1:
            trainerbattle_no_intro(TRAINER_ALISA_1_CELSINGE, SoftwindCity_Text_AlisaDefeated2)
        case 2:
            trainerbattle_no_intro(TRAINER_ALISA_1_ECHOISE, SoftwindCity_Text_AlisaDefeated2)
    }
    end
}

//=====================================
// Local Scripts
//=====================================
script(local) SoftwindCity_OnTransition {
    setflag(FLAG_VISITED_SOFTWIND_CITY)
    call(Common_EventScript_SetupRivalGfxId)
}

script(local) SoftwindCity_AfterTherma {
    lock
    setvar(VAR_SOFTWIND_CITY_STATE, SOFTWIND_STATE_ALISA_AND_WILLOW)
    delay(60)
    addobject(WILLOW)
    applymovement(WILLOW, mWillowApproachesAfterTherma)
    waitmovement(0)
    msgbox(SoftwindCity_Text_WillowCongrats)
    closemessage
    msgbox("???: Hello?\pAh, it is you, professor.")
    closemessage
    applymovement(WILLOW, mFaceLeft)
    waitmovement(0)
    addobject(ALISA)
    setobjectxy(ALISA, 21, 32)
    applymovement(ALISA, mAlisaApproachesAfterTherma)
    waitmovement(0)
    msgbox(format("Alisa: My dear Prof. Willow!\pIt really has been far too long."))
    msgbox(format("Willow: Is that you, Alisa?\pMy, you have grown up haven't you."))
    msgbox(SoftwindCity_Text_AlisaCutTheAct)
    applymovement(WILLOW, mFaceUp)
    waitmovement(0)
    msgbox(format("Willow: … Here, {PLAYER}.\pI'll heal your Pokémon for you.\pI'm certain you can beat her.\pShe's not half the prodigy she sees herself as."))
    closemessage
    call(Common_EventScript_OutOfCenterPartyHeal)
    applymovement(WILLOW, mWalkRight)
    waitmovement(0)
    applymovement(WILLOW, mFaceLeft)
    waitmovement(0)
    copyobjectxytoperm(WILLOW)
    copyobjectxytoperm(ALISA)
    msgbox(format("Alisa: Hah!\nThis from you?\pEven with this… person… you'll never find it.\pWell then, {PLAYER}.\pWhy not show me how “special” you are?"))
    closemessage
    applymovement(ALISA, mWalkRightLookUp)
    waitmovement(0)
    switch(var(VAR_STARTER_MON)) {
        case 0:
            trainerbattle_no_intro(TRAINER_ALISA_1_VENAP, SoftwindCity_Text_AlisaDefeated2)
        case 1:
            trainerbattle_no_intro(TRAINER_ALISA_1_CELSINGE, SoftwindCity_Text_AlisaDefeated2)
        case 2:
            trainerbattle_no_intro(TRAINER_ALISA_1_ECHOISE, SoftwindCity_Text_AlisaDefeated2)
    }
    setvar(VAR_SOFTWIND_CITY_STATE, SOFTWIND_STATE_DEFEATED_ALISA)
    msgbox(SoftwindCity_Text_AlisaRage)
    closemessage
    applymovement(ALISA, mAlisaWalksOffAfterBattle)
    waitmovement(0)
    removeobject(ALISA)
    release
    end
}

//=====================================
// Text
//=====================================
text SoftwindCity_Text_TownSign {
    format(
        "Softwind City\n"
        "“Where life drifts softly, like a leaf on the wind.”"
    )
}

text SoftwindCity_Text_GymSign {
    format(
        "Softwind City Pokémon Gym\n"
        "Leader: Therma\l"
        "“A woman of history and literature!”"
    )
}

text SoftwindCity_Text_TributeMoltres {
    format(
        "A reverent message:\p"
        "“It burns through the sky\nOn wings of dazzling flame\lHerald of the spring”"
    )
}

text SoftwindCity_Text_TributeArticuno {
    format(
        "A reverent message:\p"
        "“It sings in the wind\nBringing winter to the land\lThat time may march on”"
    )
}

text SoftwindCity_Text_TributeZapdos {
    format(
        "A reverent message:\p"
        "“It shocks the world\nWith a thunderous fury\lForging a new path”"
    )
}

text SoftwindCity_Text_AlisaAboutTherma_1 {
    format(
        "???: Therma went to the library in Saharan "
        "City, which is a few towns over.\p"
        "Well, it will be when they finish building "
        "the route in between.\pBut to get there "
        "now, Therma had to go a really long "
        "way around…\pHere, I'll show you."
    )
}

text SoftwindCity_Text_AlisaAboutTherma_2 {
    format(
        "???: You can see what I mean on that.\p"
        "Anyway, why are you here?\n… …\p"
        "You want to challenge Therma, too?\n"
        "Well I guess you'll have to wait around "
        "like me.\pOr have you not beaten Froy yet?\p"
        "You could always do that instead.\pFroy has "
        "his gym in Cranewing, which is to the east "
        "of here through Chartree Forest.\p"
        "You can see where that is on the PokéNav I gave you."
    )
}

text SoftwindCity_Text_ReceivedPokenav {
	"{PLAYER} received a PokéNav."
}

text SoftwindCity_Text_DefenderHopesNotSeen {
    format(
        "???: …that isn't good.\pOphelia said she "
        "didn't want any Orion Defenders to be seen "
        "in Softwind yet.\pI hope that Rocket didn't "
        "recognize my uniform…"
    )
}

text SoftwindCity_Text_DefenderScolds {
    format(
        "???: Hey, were you the one who defeated the Rockets?\p"
        "… Well, quit sticking your nose in things you know nothing about!\p"
        "Nobody asked you for help.\p"
        "We, the Orion Defenders, were supposed to take them down and "
        "capture Balfour, but you just had to let him escape!\p"
        "What am I supposed to tell Ophelia!?\nYou'll pay for this intrusion!"
    )
}

text SoftwindCity_Text_WolseyDefeated {
    "W… what!?"
}

text SoftwindCity_Text_WolseyWillPunish {
    format (
        "Defender: {PLAYER}, is it?\nI'm Wolsey.\p"
        "Remember that name, because I'll be back to punish you some "
        "other time for your audacity.\p"
        "You hear me!?\p"
        "Until then, stay out of our way!"
    )
}

text SoftwindCity_Text_WillowCatchesUp {
    format (
        "Willow: {PLAYER}, who was that?\p… … …\nAn Orion Defender?\p"
        "That's odd, I thought that organization died out ages ago…\p"
        "But that doesn't matter.\nWhat happened in Chartree?\p"
        "Bradley rushed in there, but it was over before I could get there to help.\p"
        "… … …\n… … …\p"
        "So Team Rocket has returned…\p"
        "I wonder what they were planning to do with that ash…"
    )
}

text SoftwindCity_Text_GymDude {
    format (
        "Man: Therma has returned!\p"
        "She says she will meet with you straight away, just as soon as she deals "
        "with all of the gym challenges that have piled up in her absence."
    )
}

text SoftwindCity_Text_WillowCongrats {
    format (
        "Willow: Congratulations, {PLAYER}!\p"
        "You still have a long way to go until "
        "the Pokémon League, but it really is an "
        "achievement to even get this far.\p"
        "Now, I'd like you to meet Therma with me.\p"
        "You see, what I want from her is--"
    )
}

text SoftwindCity_Text_AlisaCutTheAct {
    format (
        "Alisa: Cut the cute act, Professor.\p"
        "I know what you're doing here, and you "
        "will not succeed.\p"
        "You've lost your touch.\nYou can't even "
        "choose your aids well anymore.\pI mean "
        "look at this child.\p"
        "They will never amount to anything.\p"
        "They're nothing like me!\p"
        "I could beat him any day of the week."
    )
}

text SoftwindCity_Text_AlisaDefeated2 {
    format (
        "I can't believe this!"
    )
}

text SoftwindCity_Text_AlisaRage {
    format (
        "Alisa: You must have gotten lucky.\p"
        "There's no wa you could have beaten me "
        "if I was at my best…\p"
        "Well, {PLAYER}, I hope you've enjoyed "
        "your little journey, because those "
        "Rockets in the forest?\p"
        "That was only the beginning.\p"
        "Team Rocket and the Orion Defenders "
        "have come back.\p"
        "There's a war on the horizon, and you "
        "had better hope your little job doesn't "
        "put you in the middle of it.\p"
        "You're getting in over your head, {PLAYER}."
        "You had better watch yourself."
    )
}

//=====================================
// Movement
//=====================================
movement mAlisaApproaches_1 {
    walk_right * 3
    walk_down
}

movement mAlisaApproaches_2 {
    walk_right
    walk_down * 3
}

movement mAlisaApproaches_3 {
    walk_down * 3
}

movement mAlisaApproaches_4 {
    walk_left
    walk_down * 3
}

movement mAlisaApproaches_5 {
    walk_left * 2
    walk_down * 3
}

movement mAlisaApproaches_6 {
    walk_left * 3
    walk_down * 2
}

movement mAlisaStartsToLeave {
    walk_up * 3
    walk_right
}

movement mAlisaFinishesLeaving {
    walk_right * 7
    set_invisible
}

movement mShiftCameraToRight {
    walk_right * 4
}

movement mShiftCameraToLeft {
    walk_left * 4
}

movement mDefenderRunsIntoTown {
    walk_fast_left * 8
    walk_in_place_slow_right
}

movement mRocketFollows {
    set_visible
    walk_fast_left * 4
}

movement mRocketReturnsToForest {
    walk_right * 4
    set_invisible
}

movement mDefenderAprroachesPlayer_1 {
    walk_left
    walk_up * 2
    walk_left * 2
}

movement mDefenderAprroachesPlayer_2 {
    walk_left
    walk_up
    walk_left * 2
}

movement mDefenderAprroachesPlayer_3 {
    walk_left
    walk_down * 1
    walk_left * 2
}

movement mDefenderWalksPastPlayer_1 {
    walk_down
    walk_left * 9
    set_invisible
}

movement mDefenderWalksPastPlayer_2 {
    walk_up
    walk_left * 9
    set_invisible
}

movement mDefenderAprroachesPlayer_2_1 {
    walk_up * 2
    walk_right * 2
}

movement mDefenderAprroachesPlayer_2_2 {
    walk_up
    walk_right * 2
}

movement mDefenderAprroachesPlayer_2_3 {
    walk_right * 2
}

movement mDefenderAprroachesPlayer_2_4 {
    walk_down * 1
    walk_right * 2
}

movement mWillowApproachesDefender {
    walk_right * 3
}

movement mDefenderRunsOff {
    walk_fast_left * 9
}

movement mDefenderRunsOff2 {
    walk_fast_left * 2
    walk_fast_down
    walk_fast_left * 7
}

movement mGymDudesApproaches1 {
    walk_down
    walk_right * 3
}

movement mGymDudesApproaches2 {
    walk_down * 2
    walk_right * 3
}

movement mGymDudesApproaches3 {
    walk_down * 3
    walk_right * 3
}

movement mGymDudesApproaches4 {
    walk_down * 2
    walk_right
    walk_down * 2
    walk_right * 2
}

movement mGymDudeRushesOff {
    walk_left * 3
    walk_fast_left * 5
}

movement mWalkRight4 {
    walk_right * 4
}

movement mWillowApproachesPlayer1 {
    walk_right * 2
    walk_up
    walk_right * 3
}

movement mWillowApproachesPlayer2 {
    walk_right * 5
}

movement mWillowApproachesPlayer3 {
    walk_right * 2
    walk_down
    walk_right * 3
}

movement mWillowApproachesPlayer4 {
    walk_right * 2
    walk_down * 2
    walk_right * 3
}

movement mWillowDeparts {
    walk_left * 9
}

movement mWillowApproachesAfterTherma {
    walk_right * 4
    walk_up
}

movement mAlisaApproachesAfterTherma {
    walk_right
    walk_down * 3
    walk_right * 5
    walk_up
    walk_right
}

movement mAlisaWalksOffAfterBattle {
    walk_down
    walk_left * 9
    set_invisible
}

movement mWalkRightLookUp {
    walk_right
    walk_in_place_slow_up
}

//=====================================
// Raw
//=====================================
